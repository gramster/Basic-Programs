10 RANDOMIZE TIMER:KEY OFF:LOCATE ,,0 'Turn off bottom screen line and cursor
15 TP=0:GW=0:SNF=-1:CHF=-1:PT=INT(RND*50)+2:CPL=0:SPD=5
20 DEF FNRD(X)=INT(X*100)/100
25 DEF FNDC=INT(6*RND+1)
26 DEF FNST=INT(RND*4+1)
27 DEF FNFC=INT(RND*13+1)
30 DIM BAND$(9),CL(9),SPIN%(3),CNT(3),BN(9,3),DUM(9),MX(4)
35 DIM CRD(10,7),HND(2,20),ROL(36),DOZB(3)
40 RESTORE
41 CLS:PRINT "You must have CAPS LOCK on for this program.":PRINT
42 PRINT "Please press the letter 'A' on your keyboard."
43 A$=INKEY$:IF A$="A" THEN GOTO 50
44 IF A$="" THEN GOTO 43
45 IF A$<>"a" THEN GOTO 42
46 PRINT "Your CAPS LOCK key is not on. Please press it."
47 PRINT :BEEP:GOTO 42
50 FOR I=0 TO 9:READ BAND$(I),CL(I):NEXT
60 DATA "BAR    ",6,"LEMON  ",14,"LIME   ",14,"PINE   ",14,"APPLE  ",10
70 DATA "ORANGE ",13,"GRAPES ",1,"CHERRY ",12,"LIBERTY",9,"JACKPOT",15
72 FOR I=1 TO 10:FOR J=1 TO 7:READ CRD(I,J):NEXT :NEXT
73 DATA 6,6,6,5,6,6,6,4,6,6,6,6,6,3,4,6,6,5,6,6,3
74 DATA 1,6,6,6,6,6,1,1,6,6,5,6,6,1,1,6,6,1,6,6,1
75 DATA 1,6,6,2,6,6,1,1,6,1,6,1,6,1,1,6,1,5,1,6,1
76 DATA 1,5,1,6,1,5,1
77 SUT$=CHR$(5)+CHR$(6)+CHR$(3)+CHR$(4)
80 SCREEN 0:WIDTH 80:COLOR 4,0,4:CLS
90 LOCATE 2,28:PRINT "$$$ PC CASINO $$$"
100 LOCATE 3,28:PRINT "================="
110 COLOR 2:LOCATE 5,8:PRINT "Welcome to the PC Casino! I am your croupier, Betty McBucks."
120 PRINT:PRINT "        We have a selection of games for you to play:"
130 BAL=INT(RND*2000)+500:HOU=INT(10000*RND)+2000
140 LOCATE 23,20:COLOR 20:PRINT "HIT 'F1' TO LEAVE A GAME":KEY (1) ON:ON KEY(1) GOSUB 10020
145 LOCATE 24,24:PRINT "'F2' TO CHANGE THE FLAGS";:KEY (2) ON:ON KEY(2) GOSUB 11080
150  GOSUB 230
155 OLDBAL=BAL
160 IF GAM=3 THEN GOTO 1000
161 IF GAM=5 THEN GOTO 3000
162 IF GAM=1 THEN GOTO 2000
163 IF GAM=2 THEN GOTO 4000
164 IF GAM=4 THEN GOTO 5000
170 CLS:PRINT "SORRY, THAT GAME ISN'T AVAILABLE YET!":GOTO 150
180 COLOR 2,0:PRINT:PRINT " Would you like to play another game Y/N?"
190 A$=INKEY$
200 IF A$="Y" THEN CLS:GOTO 150
210 IF A$<>"N" THEN GOTO 190
220 PRINT :PRINT " THANKS FOR PLAYING - YOU WERE GREAT!!":END
230 LOCATE 9,12:COLOR 1:PRINT "1 - Craps";:COLOR 2:PRINT "          Game of Nevada Dice"
240 LOCATE 11,12:COLOR 1:PRINT "2 - Reddog";:COLOR 2:PRINT "         Game of High-Card Pool"
250 LOCATE 13,12:COLOR 1:PRINT "3 - Slot Machine";:COLOR 2:PRINT "   One-armed Bandit"
260 LOCATE 15,12:COLOR 1:PRINT "4 - Roulette";:COLOR 2:PRINT "       Game of French Roulette"
270 LOCATE 17,12:COLOR 1:PRINT "5 - Blackjack";:COLOR 2:PRINT "      Game of Vingt-et-Un"
280 PRINT :COLOR 7:PRINT "            What's your pleasure? (1,2,3,4 or 5)"
290 A$=INKEY$:IF A$="" THEN GOTO 290
300 GAM=VAL(A$):IF GAM<1 OR GAM>6 THEN LOCATE 21,7:PRINT CHR$(7);"Give me that again, please!!":GOTO 290
310 RETURN
1000 REM ********************** SLOT MACHINE ********************************
1010 REM
1015 GOTO 1500
1020 FOR I=1 TO 3
1030 FOR J=0 TO 9:DUM(J)=J:NEXT
1040 FOR J=10 TO 1 STEP -1
1050 L=INT(RND*J):BN(J-1,I)=DUM(L):DUM(L)=DUM(J-1)
1060 NEXT :NEXT  'Set up the drums
1070 FOR I=1 TO 3:SPIN%(I)=INT(RND*10):NEXT
1080 COLOR 7,,1:CLS
1090 REM ---- Draw the outline
1100 FOR I=11 TO 17 STEP 2
1110 LOCATE I,15:PRINT "ллллллллллллллллллллллллллллллл"
1120 NEXT
1130 FOR I=12 TO 16 STEP 2
1140 LOCATE I,15:PRINT "л         л         л         л"
1150 NEXT
1160 LOCATE 17,46:COLOR 4:PRINT "ммммл"
1170 TM=0:GOSUB 10110
1171 FOR I=18 TO 22
1172 LOCATE I,15:PRINT "ллллллллллллллллллллллллллллллл"
1173 NEXT
1180 COLOR 4
1190 LOCATE 14,12:PRINT CHR$(26);CHR$(26);CHR$(26):LOCATE 14,46:PRINT CHR$(27);CHR$(27);CHR$(27)
1200 GOSUB 10000 'REM ----- Print the balance
1210 CNT(1)=RND*10+2:FOR I=2 TO 3:CNT(I)=CNT(I-1)+RND*10+1:NEXT  'No of spins
1220 SPN=0:HOLD=1
1230 COLOR 6:LOCATE 3,10:PRINT "HIT 'S' TO SPIN":LOCATE 5,10:PRINT "HIT 'H' TO HOLD A DRUM"
1240 IF INKEY$<>"S" GOTO 1240
1250 LOCATE 24,30:PRINT "                        ";
1260 TM=2:GOSUB 10120:GOSUB 10110
1270 BAL=BAL-1:HOU=HOU+1
1280 FOR J=0 TO 2:FOR I=HOLD TO 3
1290 NUM=(SPIN%(I)+J) MOD 10:NUM=BN(NUM,I)
1300 COLOR CL(NUM):LOCATE 12+2*J,7+10*I:PRINT BAND$(NUM)
1310 NEXT:NEXT
1320 SPN=SPN+1
1330 IF INKEY$="H" OR SPN>CNT(HOLD) THEN HOLD=HOLD+1
1340 IF HOLD>3 THEN GOTO 1360
1350 FOR I=HOLD TO 3:SPIN%(I)=(SPIN%(I)+1) MOD 10:NEXT :GOTO 1280
1360 FOR I=1 TO 3:NUM=(SPIN%(I)+1) MOD 10:SPIN%(I)=BN(NUM,I)+1:NEXT
1370 WIN=0
1380 IF SPIN%(1)=SPIN%(2) AND SPIN%(2)=SPIN%(3) THEN WIN=(SPIN%(1)+INT(1/SPIN%(1)))^3:GOTO 1430
1390 FOR I=1 TO 2:FOR J=I+1 TO 3
1400 IF SPIN%(I)=SPIN%(J) THEN WIN=SPIN%(I)^2
1410 NEXT :NEXT
1420 IF WIN=0 THEN GOTO 1200 ELSE WIN=FNRD(WIN/10+1)
1430 LOCATE 24,30:COLOR 23:PRINT USING "YOU WIN $$###.##";WIN;:BEEP:BAL=BAL+WIN:HOU=HOU-WIN:GOTO 1200
1500 COLOR 4,,2:CLS
1510 LOCATE 2,35:PRINT "SLOT MACHINE"
1520 LOCATE 3,35:PRINT "============":PRINT
1530 PRINT " Welcome to the Casino's slot machines! Each turn costs you $1.00."
1540 PRINT
1550 PRINT " To start the machine, you use the 'S' key."
1560 PRINT
1570 PRINT " To lock the leftmost spinning drum, use the 'H' key."
1580 PRINT
1590 PRINT " You may lock all of the drums, or wait until they stop of their own accord."
1600  PRINT
1610 PRINT " Winnings are paid out for matches along the central row (indicated by arrows)."
1620 PRINT
1630 PRINT " Good luck!":PRINT
1640 COLOR 3:PRINT " HIT ANY KEY TO BEGIN...."
1650 IF INKEY$="" THEN GOTO 1650
1660 GOTO 1020
2000 REM ******************** CRAPS *****************************************
2010 COLOR 11,0,6:CLS
2020 LOCATE 2,38:PRINT "** CRAPS **":LOCATE 3,38:PRINT "===========":PRINT
2030 PRINT " Welcome to the Craps table!":PRINT
2040 PRINT " Craps is played with two dice. You roll them and add the faces together,"
2050 PRINT " betting on this total.":PRINT
2060 PRINT " If, on your first roll, you roll a 7 or 11, you win."
2070 PRINT "       ( This is called a 'natural' )
2080 PRINT " If you roll a 2, 3 or 12 however, you lose."
2090 PRINT "       ( This is called 'craps' )"
2100 PRINT
2110 PRINT " Any other total leaves the game undecided, and the total becomes"
2120 PRINT " your 'point'."
2130 PRINT
2140 PRINT " You then continue rolling until you either roll the same total again and win.."
2150 PRINT "       ( This is called 'making your point' )
2160 PRINT " ..or you roll a 7, 'crap out', and lose.":PRINT
2170 COLOR 5:PRINT "   HIT ANY KEY TO BEGIN PLAYING..."
2180 IF INKEY$="" THEN GOTO 2180
2190 CLS
2200 THR=0:NM=0:GOSUB 10850 'Get the bet
2255 LOCATE 4,4:COLOR 10:PRINT "Hit 'R' to roll the dice..."
2256 IF INKEY$<>"R" THEN GOTO 2256
2259 TT=INT(RND*10+5):FOR I=1 TO TT
2260 DC1=FNDC:DC2=FNDC:DCT=DC1+DC2:GOSUB 2800
2261 NEXT
2270 LOCATE 22,10:COLOR 10:PRINT "It's a ";DC1;" and a ";DC2;" - ";DCT:LOCATE 24,12
2275 IF THR THEN GOTO 2310
2280 IF DCT=2 OR DCT=3 OR DCT=12 THEN COLOR 28:PRINT "CRAPS!!!  You lose!!";:GOTO 2600
2290 IF DCT=7 OR DCT=11 THEN COLOR 27:PRINT "A NATURAL!!! You win!!";:GOTO 2500
2300 COLOR 12:PRINT "Your point is ";DCT;:PT=DCT:THR=-1:BEEP:GOTO 2255
2310 IF DCT=PT THEN COLOR 27:PRINT "You make your point and win!!";:GOTO 2500
2320 IF DCT=7 THEN COLOR 28:PRINT "You crap out and lose!!";:GOTO 2600
2330 GOTO 2255
2500 BAL=BAL+2*NM:HOU=HOU-2*NM
2600 IF SNF THEN BEEP
2601 GOSUB 3890
2610 IF BAL<=0 THEN GOSUB 10400
2615 IF HOU<=0 THEN GOTO 11000
2620 GOTO 2190
2800 COLOR 4:DC=DC1:X=10:GOSUB 2850:COLOR 2:DC=DC2:X=25:GOSUB 2850:RETURN
2850 LOCATE 12,X
2852 IF DC=1 THEN PRINT "ллллл":GOTO 2860
2854 IF DC>3 THEN PRINT "л";CHR$(8);"л";CHR$(8);"л":GOTO 2860
2856 PRINT "л";CHR$(8);"ллл"
2860 LOCATE 13,X
2862 IF DC=6 THEN PRINT "л";CHR$(8);"л";CHR$(8);"л":GOTO 2870
2864 IF DC MOD 2>0 THEN PRINT "лл";CHR$(8);"лл":GOTO 2870
2866 PRINT "ллллл"
2870 LOCATE 14,X
2872 IF DC=1 THEN PRINT "ллллл":GOTO 2880
2874 IF DC>3 THEN PRINT "л";CHR$(8);"л";CHR$(8);"л":GOTO 2880
2876 PRINT "ллл";CHR$(8);"л"
2880 RETURN
3000 REM ******************* BLACKJACK ***************************
3010 COLOR 12,0,7:CLS:SHF=12
3020 LOCATE 2,38:PRINT "** BLACKJACK **":LOCATE 3,38:PRINT "===============":PRINT
3030 PRINT " Welcome to the Blackjack table!"
3040 PRINT:PRINT " We will play with a full deck of cards. May I remind you of the rules..."
3050 PRINT :PRINT " All face cards count 10 points. Aces count either 1 or 11 (you decide), and"
3060 PRINT " all other cards count their numerical value.":PRINT
3070 PRINT " I will deal each of us two cards, one of mine face up."
3080 PRINT " The object is to get a total of 21, or as close to 21 as possible, without"
3090 PRINT " actually exceeding 21. If your card total exceeds 21, you are 'bust' and lose."
3100 PRINT " After examining your cards, you may draw more cards if you wish."
3110 PRINT " When I ask you what you want to do, you must say whether you wish to draw more"
3120 PRINT " cards ('hit') or stop drawing cards ('stand')."
3130 PRINT " If your first two cards total 21, you have a 'natural'."
3140 PRINT :PRINT " Betting takes place before the deal."
3150 PRINT " If you have a natural, you win 1.5 times your bet, unless I also have one,"
3160 PRINT " in which case your bet is returned."
3170 PRINT :COLOR 7:PRINT "       HIT ANY KEY TO START PLAYING....";
3180 IF INKEY$="" THEN GOTO 3180 ELSE CLS
3190 GOSUB 10750:GOSUB 10850:GOSUB 10895:GOSUB 10900
3194 NAT1=0:NAT2=0
3195 IF HND(2,1)=1 AND HND(2,3)>9 THEN NAT1=-1
3196 IF HND(2,1)>9 AND HND(2,3)=1 THEN NAT1=-1
3197 IF HND(2,2)=1 AND HND(2,4)>9 THEN NAT2=-1
3198 IF HND(2,2)>9 AND HND(2,4)=1 THEN NAT2=-1
3200 CAR1=1:CAR2=2:CRD1=3:CRD2=4:STK=0
3210 FOR CR=CAR1 TO CRD1 STEP 2:GOSUB 3300:IF NOT (STK) AND CR=1 THEN FACE=0
3215 GOSUB 10499:NEXT:IF STK THEN CAR1=CRD1:GOTO 3235
3220 FOR CR=CAR2 TO CRD2 STEP 2:GOSUB 3300:GOSUB 10499:NEXT:CAR2=CRD2
3230 PLAYER=1:CRDS=CRD2:GOSUB 11010:VAL2=PIPS
3235 PLAYER=0:CRDS=CRD1:GOSUB 11010:VAL1=PIPS:ACE1=ACES:ACUS=AC
3240 LOCATE 24,20:COLOR 7,0:PRINT " TOTAL OF ";VAL2;:IF NAT2 THEN PRINT " - A NATURAL!";
3245 IF STK THEN LOCATE 12,20:COLOR 7,0:PRINT " TOTAL OF ";VAL1;:IF NAT1 THEN PRINT " - A NATURAL!"
3246 IF VAL2>21 THEN GOTO 3800
3247 IF VAL1>21 THEN GOTO 3900
3248 IF NAT2 THEN STK=-1
3249 IF STK THEN GOTO 3500
3250 GOSUB 3400:PRINT "Hit or Stick? ('H' or 'S')";
3260 A$=INKEY$:IF A$="" THEN GOTO 3260
3270 IF A$="S" THEN STK=-1:GOTO 3210
3280 IF A$="H" THEN CRD2=CRD2+2:CAR2=CRD2:GOTO 3220
3290 GOTO 3260
3300 FACE=HND(2,CR):SUIT=HND(1,CR):RETURN
3400 LOCATE 25,38:COLOR 15,0:PRINT "                              ";:LOCATE 25,38:RETURN
3500 GOSUB 3400
3502 IF NAT2 THEN GOTO 3602
3505 IF ACE1>ACUS AND VAL1<18 THEN GOTO 3520
3506 IF VAL1<17 AND INT(RND*5)=0 THEN GOTO 3520
3510 IF VAL1>15 THEN GOTO 3600
3520 PRINT "I HIT -------";:CRD1=CRD1+2:CAR1=CRD1
3530 GOSUB 3890:GOTO 3210
3600 PRINT "I STICK -----";:GOSUB 3890
3602 IF NAT2 AND NOT(NAT1) THEN BAL=BAL+NM/2:HOU=HOU-NM/2:GOTO 3910
3603 IF NAT1 AND NOT(NAT2) THEN GOTO 3810
3605 IF NAT1 AND NAT2 THEN GOTO 3620
3610 IF VAL1=VAL2 THEN GOSUB 3400:PRINT "--- A DRAW - PLAY AGAIN ---";:BAL=BAL+NM:GOSUB 3890:GOSUB 3400:GOTO 3999
3615 IF VAL1<VAL2 THEN GOTO 3910 ELSE GOTO 3810
3620 GOSUB 3400:PRINT "---- A DRAW ----";:GOSUB 3890:GOSUB 3400
3630 BAL=BAL+NM:HOU=HOU-NM:GOTO 3999
3800 GOSUB 3400:PRINT "You bust!!";:GOSUB 3890
3810 GOSUB 3400:PRINT "----- I WIN -----";
3820 IF BAL<=0 THEN GOSUB 10400
3830 GOTO 3990
3890 FOR DL=0 TO SPD*SPD:SOUND 32767,1:NEXT :RETURN
3900 GOSUB 3400:PRINT "I bust!!";:GOSUB 3890
3910 GOSUB 3400:PRINT "----- YOU WIN -----";
3930 BAL=BAL+NM+NM:HOU=HOU-NM-NM
3990 GOSUB 3890:GOSUB 3400:NM=0
3992 IF HOU<=0 THEN GOTO 11000
3994 IF BAL<=0 THEN GOSUB 10400
3999 CLS:GOTO 3190
4000 REM **************************** REDDOG ********************************
4005 COLOR 8,0,5:CLS:SHF=12:CPL=0:POT=0
4010 LOCATE 2,38:PRINT "*** REDDOG ***":LOCATE 3,38:PRINT "==============":PRINT
4015 PRINT " Welcome to the table! Let me explain how to play.":PRINT
4020 GOSUB 4999
4025 PRINT " I will deal you five cards, and then you bet. The object is to beat the card"
4030 PRINT " on top of the stock. To do this, you must have a card of the same suit but a"
4035 PRINT " higher face value. If you win, the money is paid out of the pot, and if you"
4040 PRINT " lose, your bet goes into the pot. As all winnings are paid out of the pot,"
4045 PRINT " may not bet more than the amount in the pot. If the pot is emptied, we both"
4050 PRINT " must ante again. After you have played, I play."
4055 PRINT " At the end of the game, we split the pot.":PRINT
4056 PRINT " You have to bet at least $1.00. Note that aces are high.":PRINT
4060 COLOR 4:PRINT "          HIT ANY KEY TO BEGIN...."
4065 IF INKEY$="" THEN GOTO 4065 ELSE GOSUB 4999
4066 HOU=HOU-PT:POT=POT+PT:IF HOU<0 THEN HOU=HOU+PT:CLS:PRINT "I don't have enough to play!":GOSUB 3890:POT=0:GOSUB 10020
4067 BAL=BAL-PT:POT=POT+PT:IF BAL<0 THEN BAL=BAL+PT:CLS:PRINT "You don't have enough to play!":GOSUB 3890:POT=0:GOSUB 10020
4070 CLS:GOSUB 10750:GOSUB 10895
4075 GOSUB 4995:FOR I=1 TO 4:MX(I)=0:NEXT:GOSUB 10000
4080 NM=0:IF CPL THEN GOSUB 4980 ELSE GOSUB 4990
4100 GOSUB 3890
4110 CR=TP:IF CPL THEN LOCATE 24,22 ELSE LOCATE 12,22
4120 PRINT "TOP CARD";:X=22:Y=2: IF CPL THEN Y=14
4130 GOSUB 3300:GOSUB 10500:IF FACE=1 THEN FACE=14
4140 WIN=0:IF MX(SUIT)>FACE THEN WIN=-1
4150 COLOR 15:LOCATE 25,20:PRINT "*** ";:IF CPL THEN PRINT "I "; ELSE PRINT "YOU ";
4160 IF WIN THEN PRINT "WIN"; ELSE PRINT "LOSE";
4170 PRINT " ***";:GOSUB 3890:CPL=-1-CPL:LOCATE 25,20:PRINT "                ";
4175 IF CPL THEN LOCATE 12,22 ELSE LOCATE 24,22
4176 PRINT SPACE$(8);
4180 IF NOT(WIN) THEN NM=-NM
4185 POT=POT-NM
4190 IF NOT(CPL) THEN HOU=HOU+NM ELSE IF NM>0 THEN BAL=BAL+NM+NM
4200 IF POT<1 THEN LOCATE 1,1:BEEP:GOSUB 4999:GOTO 4066
4210 IF BAL<=0 THEN GOSUB 10400
4220 IF HOU<=0 THEN GOTO 11000
4230 IF CPL THEN GOTO 4075 ELSE GOTO 4070
4950 NM=0:GOSUB 10860:GOSUB 10895:IF NM<=POT THEN RETURN
4960 LOCATE 2,2:COLOR 15:PRINT USING "You can't bet more than $####.##";FNRD(POT):GOSUB 3890:GOSUB 10895:GOTO 4950
4980 FOR I=13 TO 24:LOCATE I,1:PRINT SPACE$(54);:NEXT:GOSUB 10911
4982 FOR CR=1 TO 9 STEP 2:GOSUB 3300:GOSUB 10499:IF FACE=1 THEN FACE=14
4983 IF MX(SUIT)<FACE THEN MX(SUIT)=FACE
4984 NEXT:TP=11:NM=0
4985 FOR I=1 TO 4:NM=NM+MX(I):NEXT:NM=FNRD((PT+2)*(NM-3)/45)
4986 IF NM<1 THEN NM=1
4987 IF NM>PT THEN NM=PT
4988 IF NM>HOU THEN NM=HOU
4989 GOSUB 10892:RETURN
4990 GOSUB 10912
4992 FOR CR=2 TO 10 STEP 2:GOSUB 3300:GOSUB 10499:IF FACE=1 THEN FACE=14
4993 IF MX(SUIT)<FACE THEN MX(SUIT)=FACE
4994 NEXT:TP=12:GOTO 4950
4995 LOCATE 7,70:COLOR 15:PRINT "оппппппппн":LOCATE 8,70:PRINT "о $      н":LOCATE 9,70:PRINT "оммммммммн":LOCATE 8,73:PRINT INT(POT)
4996 COLOR 14:LOCATE 6,71:PRINT "The Pot":RETURN
4999 PRINT " We must each put $";PT;" into the pot to play.":GOSUB 3890:RETURN
5000 REM ************************ ROULETTE ****************************
5010 COLOR 3,,2:CLS:LOCATE 2,38:PRINT "/// ROULETTE \\\":LOCATE 3,38:PRINT "================":PRINT
5020 PRINT " Welcome to the Roulette table!":PRINT :PRINT " I will spin the wheel - you bet on the outcome of the spin."
5030 PRINT " You can bet on individual numbers, high numbers (19-36), low numbers (1-18),"
5040 PRINT " odds (1,3..,35), evens (2,4,..,36), or a dozen (1-12, 13-24, 25-36)."
5050 PRINT :PRINT " You only win on zero (0) if you bet on it.":PRINT
5060 COLOR 7:PRINT "       HIT ANY KEY TO START PLAYING...."
5070 A$=INKEY$:IF A$="" THEN GOTO 5070
5080 CLS:FOR I=0 TO 36:ROL(I)=0:NEXT:FOR I=1 TO 3:DOZB(I)=0:NEXT:ODD=0:EVE=0:HI=0:LO=0
5085 COLOR 6:LOCATE 10,10:PRINT "лллллл":LOCATE 12,10:PRINT "лллллл":LOCATE 11,10:PRINT "л    л"
5090 GOSUB 10000
5100 COLOR 3:GOSUB 10895:LOCATE 2,2:PRINT "Type of bet? (O-dds, E-vens, H-igh, L-ow, D-ozen, N-umber)":LOCATE 3,2:PRINT "Press 'S' to finish betting and spin wheel"
5110 A$=INKEY$:IF A$="" THEN GOTO 5110
5115 IF A$="S" THEN GOTO 5200
5120 IF A$="O" OR A$="E" OR A$="H" OR A$="L" THEN GOTO 5180
5130 IF A$<>"D" AND A$<>"N" THEN GOTO 5110
5140 IF A$="N" THEN GOTO 5160
5142 GOSUB 10895:LOCATE 2,2:PRINT "Which dozen? (1:1-12; 2:13-24; 3:25-36)";
5144 DOZ=VAL(INKEY$):IF DOZ>3 OR DOZ<1 THEN GOTO 5144
5146 NM=0:GOSUB 10850:DOZB(DOZ)=DOZB(DOZ)+NM:GOTO 5100
5160 GOSUB 10895:LOCATE 2,2:PRINT "Which number?";:GOSUB 10199:NUM=NM:NM=0
5162 IF NUM>36 THEN GOTO 5100
5164 GOSUB 10850:ROL(NUM)=ROL(NUM)+NM:GOTO 5100
5180 NM=0:GOSUB 10850:IF A$="O" THEN ODD=ODD+NM:GOTO 5100
5182 IF A$="E" THEN EVE=EVE+NM:GOTO 5100
5184 IF A$="H" THEN HI=HI+NM:GOTO 5100
5186 LO=LO+NM:GOTO 5100
5200 GOSUB 10895:NUM=INT(RND*37):SPIN=15+INT(RND*25):COLOR 2
5210 FOR I=1 TO SPIN:N=INT(RND*37):LOCATE 11,12:PRINT USING "##";N
5220 IF SNF THEN SOUND 100,1:SOUND 110,1
5225 FOR DL=0 TO SPD:SOUND 32767,1:NEXT
5230 NEXT :LOCATE 11,12:PRINT USING "##";NUM:IF SNF THEN BEEP
5240 AM=0:IF NUM MOD 2=0 THEN GOTO 5250
5242 IF ODD>0 THEN AM=2*ODD:M$="ODD NUMBERS":GOSUB 5900:GOTO 5260
5250 IF EVE>0 THEN AM=2*EVE:M$="EVEN NUMBERS":GOSUB 5900
5260 IF NUM>18 THEN GOTO 5270
5262 IF LO>0 THEN AM=2*LO:M$="LOW NUMBERS":GOSUB 5900:GOTO 5280
5270 IF HI>0 THEN AM=2*HI:M$="HIGH NUMBERS":GOSUB 5900
5280 IF ROL(NUM)>0 THEN AM=36*ROL(NUM):M$="THE NUMBER ITSELF":GOSUB 5900
5290 IF NUM<13 AND DOZB(1)>0 THEN AM=3*DOZB(1):M$="BOTTOM DOZEN":GOSUB 5900:GOTO 5400
5300 IF NUM<25 AND DOZB(2)>0 THEN AM=3*DOZB(2):M$="THE MIDDLE DOZEN":GOSUB 5900:GOTO 5400
5310 IF DOZB(3)>0 THEN AM=3*DOZB(3):M$="THE TOP DOZEN":GOSUB 5900
5320 IF AM=0 THEN GOSUB 5900
5400 IF BAL<=0 THEN GOSUB 10400
5410 IF HOU<=0 THEN GOTO 11000
5420 GOTO 5080
5900 LOCATE 5,10:COLOR 15:AM=FNRD(AM)
5910 IF AM>0 THEN M$="YOU WON $"+STR$(AM)+" ON "+M$ ELSE M$="YOU LOST IT ALL!"
5920 LOCATE 5,5:PRINT M$:HOU=HOU-AM:BAL=BAL+AM
5925 IF SNF THEN SOUND 300,2
5930 GOSUB 3890:LOCATE 5,5:PRINT SPACE$(50):RETURN
10000 COLOR 12:AM=1000:DBAL=BAL:LC=55:LOCATE 24,62:PRINT USING "$$#####,.##";BAL;
10005 IF NOT(CHF) THEN RETURN
10010 GOSUB 10060:AM=AM/10:IF AM>=1 THEN GOTO 10010 ELSE RETURN
10020 CLS:IF GAM=2 THEN POT=FNRD(POT/2):PRINT USING "We split the pot : $$####.## each";POT:BAL=BAL+POT:HOU=HOU+POT
10025 PRINT:PRINT " You ";:IF OLDBAL>BAL THEN PRINT USING "lost $$#####,.##";OLDBAL-BAL;
10030 IF OLDBAL=BAL THEN PRINT "broke even";
10040 IF OLDBAL<BAL THEN PRINT USING "won $$#####,.##";BAL-OLDBAL;
10050 PRINT " in that game!":CPL=0:RETURN 180
10060 AMM=INT(DBAL/AM):LC=LC+5:COLOR (LC-55)/5
10070 DBAL=DBAL-AMM*AM:LOCATE 22,LC:PRINT USING "####";AM
10080 FOR CHP=1 TO 10:LOCATE 21-CHP,LC:PRINT "    ":NEXT
10090 IF AMM>0 THEN FOR CHP=1 TO AMM:LOCATE 21-CHP,LC:PRINT "мммм":NEXT
10100 RETURN
10110 COLOR 4:FOR LC=16 TO 12 STEP -1:LOCATE LC,50:IF SNF THEN SOUND 67,TM
10115 PRINT "л":NEXT :RETURN
10120 COLOR 4:FOR LC=12 TO 16:LOCATE LC,50:IF SNF THEN SOUND 67,TM
10125 PRINT " ":NEXT :RETURN
10199 NM=0
10200 NM$=""
10210 AA$=INKEY$:IF AA$="" THEN GOTO 10210
10215 IF ASC(AA$)=13 THEN PRINT :NM=NM+VAL(NM$):RETURN
10216 IF ASC(AA$)=8 THEN GOTO 10240
10220 IF AA$="." OR (ASC(AA$)>47 AND ASC(AA$)<58) THEN 10230 ELSE 10210
10230 NM$=NM$+AA$:PRINT AA$;:GOTO 10210
10240 IF LEN(NM$)=0 THEN GOTO 10210
10250 NM$=LEFT$(NM$,LEN(NM$)-1):PRINT CHR$(29);" ";CHR$(29);:GOTO 10210
10400 CLS:COLOR 14:PRINT:PRINT " You seem to be short of money, dear chap!"
10410 PRINT :IF TP THEN GOTO 10450
10420 PRINT " Would you like to sell me your gold tie-pin?"
10430 GOSUB 10495
10432 IF A$="N" THEN GOTO 10450
10433 TP=-1
10436 IF RND>.5 THEN GOTO 10440
10437 PRINT " Hmmm. It's only a cheap trinket! You can have $40.00":BAL=BAL+40:HOU=HOU-40:GOTO 10490
10440 PRINT " Not bad! I'll give you $120.00":BAL=BAL+120:HOU=HOU-120:GOTO 10490
10450 IF GW THEN PRINT "..and you";:GOTO 10498
10460 PRINT " Would you like to sell your watch?"
10470 GOSUB 10495
10480 IF A$="N" THEN PRINT "You";:GOTO 10498
10485 GW=-1:GOTO 10436
10490 PRINT :PRINT " Hit any key to return to the game..."
10493 IF INKEY$="" THEN GOTO 10493 ELSE RETURN
10495 A$=INKEY$:IF A$<>"Y" AND A$<>"N" THEN GOTO 10495 ELSE RETURN
10498 PRINT " have nothing left to sell! Get out of here, you pauper!":PRINT :END
10499 X=INT((CR+1)/2)*10-8:Y=14-12*(CR MOD 2)
10500 CL=0:IF SUIT>2 THEN CL=4
10505 IF SNF THEN SOUND 1000,3
10510 LOCATE Y,X:COLOR CL,7:S$=MID$(SUT$,SUIT,1)
10511 IF FACE=0 THEN Q$=" ":R$=" ":GOTO 10520
10512 Q$=MID$("A234567891JQK",FACE,1)
10513 IF FACE>10 THEN R$=MID$("JQK",FACE-10,1):Q$=" "
10520 IF FACE=10 THEN PRINT "  10     " ELSE PRINT "  ";Q$;"      "
10530 IF FACE>10 THEN GOTO 10670
10540 FOR J=1 TO 7:LOCATE Y+J,X:IF FACE=0 THEN GOTO 10605
10545 ON CRD(FACE,J) GOTO 10550,10560,10570,10580,10590,10600
10550 PRINT " ";S$;"     ";S$;" ":GOTO 10610
10560 PRINT " ";S$;"  ";S$;"  ";S$;" ":GOTO 10610
10570 PRINT "       ";S$;" ":GOTO 10610
10580 PRINT " ";S$;"       ":GOTO 10610
10590 PRINT "    ";S$;"    ":GOTO 10610
10600 PRINT "         ":GOTO 10610
10605 COLOR 4:PRINT "ВВВВВВВВВ"
10610 NEXT:Y=Y+7
10620 GOTO 10690
10670 Y=Y+1:LOCATE Y,X:PRINT " ";R$;"     ";S$;" ":FOR JJ=1 TO 5:LOCATE Y+JJ,X:PRINT "         ":NEXT
10680 Y=Y+6:LOCATE Y,X:PRINT " ";S$;"     ";R$;" "
10690 Y=Y+1:LOCATE Y,X:IF FACE=10 THEN PRINT "     10  " ELSE PRINT "      ";Q$;"  "
10700 COLOR,0:RETURN
10750 LOCATE 2,2:COLOR 7:PRINT "PLEASE WAIT WHILE I SHUFFLE THE CARDS....";
10760 HND(1,1)=FNST:HND(2,1)=FNFC:FOR I=2 TO SHF
10770 HND(1,I)=FNST:HND(2,I)=FNFC
10780 J=1
10790 IF HND(1,I)=HND(1,J) AND HND(2,I)=HND(2,J) THEN GOTO 10770
10800 J=J+1:IF J<I THEN GOTO 10790
10810 NEXT:RETURN
10850 GOSUB 10000 'Print balance
10860 GOSUB 10895:LOCATE 2,2:COLOR 11:PRINT "What's your bet? $";:GOSUB 10200
10862 BAL=FNRD(BAL):HOU=FNRD(HOU):NM=FNRD(NM)
10865 IF NM<1 AND BAL>1 THEN LOCATE 4,2:COLOR 7:PRINT "Don't be ridiculous!!":SOUND 32767,90:GOTO 10860
10870 IF BAL>=NM THEN GOTO 10890
10880 LOCATE 4,2:COLOR 7:PRINT "You can't bet what you haven't got!":BEEP:GOSUB 3890:NM=NM-VAL(NM$):GOTO 10860
10890 BAL=BAL-NM:HOU=HOU+NM
10891 LOCATE 2,72:COLOR 14:PRINT "Your Bet"
10892 IF CPL THEN LOCATE 2,72:COLOR 14:PRINT " My Bet "
10893 COLOR 15:LOCATE 3,70:PRINT "оппппппппн":LOCATE 4,70:PRINT "о $      н":LOCATE 5,70:PRINT "оммммммммн":LOCATE 4,73:PRINT INT(NM):RETURN
10895 FOR I=2 TO 4:LOCATE I,2:PRINT SPACE$(58):NEXT :RETURN
10900 GOSUB 10911:GOSUB 10912:RETURN
10911 LOCATE 12,10:COLOR 2:PRINT "MY HAND":RETURN
10912 LOCATE 24,10:COLOR 9:PRINT "YOUR HAND";:RETURN
11000 CLS:PRINT "You have broken the bank you pig!!":END
11010 PIPS=0:ACES=0
11020 FOR LP=1+PLAYER TO CRDS STEP 2
11030 IF HND(2,LP)<11 THEN PIPS=PIPS+HND(2,LP) ELSE PIPS=PIPS+10
11040 IF HND(2,LP)=1 THEN ACES=ACES+1
11050 NEXT :AC=ACES
11060 IF AC=0 OR PIPS>11 THEN RETURN
11070 AC=AC-1:PIPS=PIPS+10:GOTO 11060
11080 HZ=POS(0):VT=CSRLIN
11081 LOCATE 1,1:PRINT " SPEED (0-9)"
11082 A$=INKEY$:IF VAL(A$)>=0 AND VAL(A$)<10 THEN SPD=VAL(A$):GOTO 11090
11084 GOTO 11082
11090 LOCATE 1,1:COLOR 7:PRINT " SOUND (1)-OFF (2)-ON"
11091 A$=INKEY$:SNF=-1:IF A$="1" THEN SNF=0:GOTO 11095
11092 IF A$<>"2" THEN GOTO 11091
11095 LOCATE 1,1:PRINT " CHIP DISPLAY (1)-OFF (2)-ON":CHF=-1
11096 A$=INKEY$:IF A$="1" THEN CHF=0:GOTO 11099
11097 IF A$<>"2" THEN GOTO 11096
11099 LOCATE 1,1:PRINT "                             ":LOCATE VT,HZ:RETURN

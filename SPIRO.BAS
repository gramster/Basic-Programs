10 DEF FNRAD(X)=X*.0174533
20 CLS:PRINT "     SPIROGRAPH":PRINT "     ==========":PRINT :PRINT
30 INPUT "FIXED WHEEL RADIUS";FR:IF FR>199 THEN PRINT "TOO BIG!!!":GOTO 30
40 INPUT "MOVING WHEEL RADIUS";MR
50 INPUT "HOLE DISTANCE FROM CENTRE OF MOVING WHEEL";HD:IF HD>MR THEN PRINT "CANT BE BIGGER THAN THE RADIUS!!":GOTO 50
60 INPUT "ROTATE INSIDE OR OUTSIDE OF FIXED WHEEL";A$:A$=LEFT$(A$,1):S=1:IF A$="I" THEN S=-1
70 D=FR+S*MR:IF D+MR>199 THEN PRINT "WHEELS TOO BIG FOR SCREEN":GOTO 30
80 PRINT "SELECT COLOUR":PRINT "1 - GREEN":PRINT "2 - RED":PRINT "3 - BROWN":PRINT "4 - CYAN":PRINT "5 - MAGENTA":PRINT "6 - WHITE"
90 INPUT "NUMBER";CL:PL=0:IF CL>3 THEN CL=CL-3:PL=1
100 PRINT "PRESS RETURN TO BEGIN":INPUT A$
110 THET1=0:THET2=0:DT=FR/MR+S:SCREEN 1:CLS:COLOR 0,PL:PRESET (160,100+D+HD):DT=2*DT*S
120 A1=FNRAD(THET1):A2=FNRAD(THET2)
130 X=D*SIN(A1)+HD*SIN(A2)+160
140 Y=D*COS(A1)+HD*COS(A2)+100 'DIVIDE BY 2 TO CORRECT DISTORTION
150 LINE -(X,Y),CL
160 THET1=THET1+2:THET2=THET2+DT
170 IF THET1=360 THEN THET1=0
180 IF THET2<0 THEN THET2=THET2+360:GOTO 180
190 IF THET2>=360 THEN THET2=THET2-360
200 IF THET1>0 THEN GOTO 120
210 IF THET2>.5 THEN GOTO 120
220 INPUT A$:RUN

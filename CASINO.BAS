10 REM ------------------------ CASINO ------------------------------------
20 DIM A9(36),A1(20),A(4,13),D(20),E(20),P(2),W1(4,2),W(4,2),D1(5)
30 DIM Y(4),K(5),Y1(4)
40 DIM Z$(10),D$(20),C$(20),P$(2),W$(2)
50 R$=TIME$:HR=VAL(LEFT$(R$,2)):MN=VAL(MID$(R$,4,2)):SC=VAL(RIGHT$(R$,2))
60 RANDOMIZE TIMER
70 DEF FNA(X)=INT(X*100+.5)/100
80 H=FNA(10000*RND)+1000
90 C=FNA(2000*RND)+200
100 Z9=0
110 G1=5
120 PRINT "WELCOME TO THE CASINO!!":PRINT "I AM YOUR CROUPIER, LINDA LUSCIOUS.":PRINT "WE HAVE A SELECTION OF GAMES FOR YOU TO PLAY."
130 GOTO 330
140 PRINT "#....NAME....................EXPLANATION........................."
150 PRINT "-----------------------------------------------------------------"
160 PRINT "1 CRAPS          DICE GAME"
170 PRINT "2 ROULETTE       FRENCH ROULETTE"
180 PRINT "3 BANDIT         SLOT MACHINE"
190 PRINT "4 BLACKJACK      GAME OF 21 (PONTOON, VING-ET-UN)"
200 PRINT "5 REDDOG         CARD GAME"
210 PRINT "6 POKER          DRAW POKER"
220 PRINT "-----------------------------------------------------------------"
230 INPUT "WHAT'S YOUR PLEASURE? (ENTER THE NUMBER)";Z8:Z9=Z9+1
240 ON Z8 GOTO 350,780,1440,1770,3230,4140
250 PRINT "GIVE ME THAT ONE AGAIN":GOTO 140
260 C=FNA(C):IF C<=0 THEN GOSUB 650
270 PRINT USING "IF YOU HAVEN'T BEEN INFORMED YET YOU NOW HAVE R#####.##";C
280 IF Z9<=4 THEN GOTO 300
290 PRINT "THE CASINO IS BEING RAIDED!!!":PRINT "YOU HAD BETTER LEAVE FAST BEFORE YOU GET LOCKED UP!":END
300 INPUT "WOULD YOU LIKE TO PLAY ANOTHER GAME";A$:IF A$="Y" THEN GOTO 340
310 IF A$="N" THEN PRINT "TOO BAD! THANKS FOR PLAYING.":PRINT "GOODBYE.":END
320 GOTO 300
330 PRINT USING "YOU START WITH R####.##";C
340 INPUT "DO YOU WANT TO SEE THE GAMES";A$:IF A$="Y" THEN GOTO 140 ELSE GOTO 230
350 REM --------------- CRAPS ---------------------------------------------
360 PRINT " *** CRAPS ***":INPUT "DO YOU NEED INSTRUCTIONS";A$
370 IF A$="Y" THEN GOTO 700
380 GOSUB 580
390 INPUT "HOW MUCH DO YOU WANT TO BET";B:B=FNA(B)
400 IF B<=0 THEN GOTO 610
410 C=C-B:IF C<0 THEN GOTO 620
420 K=INT(6*RND+1):L=INT(6*RND+1):M=L+K
430 IF M=7 OR M=11 THEN GOTO 530
440 IF M=2 OR M=3 OR M=12 THEN GOTO 540
450 PRINT "IT'S A ";K;" AND A ";L;", YOUR POINT IS ";M
460 INPUT "TYPE 'R' TO ROLL AGAIN";A$:IF A$<>"R" THEN GOTO 460
470 K=INT(RND*6+1):L=INT(RND*6+1):PRINT "IT'S A ";K;" AND A ";L;" , ";K+L
480 IF K+L=M THEN GOTO 510
490 IF K+L=7 THEN GOTO 520
500 GOTO 460
510 PRINT "YOU GOT YOUR POINT!":GOTO 560
520 PRINT "CRAPS! YOU DIDN'T GET YOUR POINT.":GOTO 550
530 PRINT "IT'S A ";K;" AND A ";L;" , ";K+L;" ...A NATURAL!":GOTO 560
540 PRINT "IT'S A ";K;" AND A ";L;" , ";K+L;" ...CRAPS!"
550 PRINT USING "YOU LOSE YOUR R#####.##";B:H=H+B:GOTO 570
560 PRINT USING "YOU WIN R#####.##";B:C=C+2*B:H=H-B:IF H<=0 THEN GOTO 690
570 GOSUB 580:GOTO 590
580 PRINT USING "YOU NOW HAVE R#####.##";C:RETURN
590 INPUT "PLAY AGAIN";A$:C=FNA(C):IF A$="N" THEN GOTO 600 ELSE GOTO 390
600 PRINT "TOO BAD! GOODBYE.":GOTO 260
610 PRINT "LOUSY BET. TRY AGAIN...":GOTO 390
620 C=FNA(C+B):PRINT "YOU CAN'T BET WHAT YOU HAVEN'T GOT!"
630 IF C<=0 THEN GOSUB 650
640 GOTO 580
650 IF G1=0 THEN GOTO 680
660 INPUT "WOULD YOU LIKE TO SELL ONE OF YOUR KRUGERRANDS";A$:IF A$="N" THEN GOTO 680
670 PRINT "YOU ARE NOW R100.00 RICHER":C=C+100:G1=G1-1:RETURN
680 PRINT "YOUR WAD IS SHOT! SO LONG, SUCKER!":END
690 PRINT "YOU HAVE BROKEN THE BANK!! BOO-HOO!":GOSUB 580:END
700 PRINT "CRAPS IS PLAYED WITH TWO DICE."
710 PRINT "THE CROUPIER ROLLS THEM AND ADDS THE FACES TOGETHER."
720 PRINT "YOU BET ON THIS TOTAL."
730 PRINT "IF YOU ROLL A 7 OR 11 (A NATURAL), YOU WIN."
740 PRINT "IF YOU ROLL A 2, 3 OR 12 (CRAPS), YOU LOSE."
750 PRINT "ANY OTHER ROLL IS UNDECIDED, AND YOU ROLL AGAIN UNTIL:"
760 PRINT "1. YOU ROLL THE SAME NUMBER AGAIN AND WIN OR"
770 PRINT "2. YOU ROLL A 7, CRAP OUT, AND LOSE.":GOTO 380
780 REM ---------------------- ROULETTE -----------------------------------
790 FOR I=1 TO 36:A9(I)=37:NEXT
800 O1=2
810 PRINT "THIS IS THE GAME OF ROULETTE.":INPUT "DO YOU NEED INSTRUCTIONS";A$
820 IF A$="Y" THEN GOTO 1250
830 INPUT "WOULD YOU LIKE TO BET ON THE NUMBER";A$:IF A$="N" THEN GOTO 870
840 INPUT "HOW MANY NUMBERS (1-36)";A1:IF A1<1 OR A1>36 THEN GOTO 830
850 FOR I=1 TO A1:PRINT "WHAT IS YOUR NO ";I;" NUMBER";:INPUT A9(I):NEXT
860 O1=36/A1:GOTO 1010
870 INPUT "WHAT WOULD YOU LIKE TO BET ON";A$
880 IF A$="H" THEN GOTO 930
890 IF A$="L" THEN GOTO 940
900 IF A$="E" THEN GOTO 950
910 IF A$="O" THEN GOTO 960
920 IF A$="D" THEN GOTO 970 ELSE GOTO 1230
930 FOR I=19 TO 36:A9(I)=I:NEXT :GOTO 1010
940 FOR I=1 TO 18:A9(I)=I:NEXT:GOTO 1010
950 FOR I=1 TO 18:A9(I)=I*2:NEXT :GOTO 1010
960 FOR I=1 TO 18:A9(I)=I*2-1:NEXT:GOTO 1010
970 INPUT "WHICH DOZEN? 1=(1-12) 2=(13-24) 3=(25-36)";A1:IF A1<1 OR A1>3 THEN GOTO 1240
980 A1=A1*12-12
990 FOR I=1 TO 12:A9(I)=A1+I:NEXT
1000 O1=3
1010 INPUT "WHAT IS YOUR BET";B:B=FNA(B):IF B<=0 THEN GOTO 1220
1020 C=C-B:IF C<0 THEN GOTO 1190
1030 REM ---- SPIN WHEEL
1040 W=INT(37*RND(0)):PRINT "IT'S A ";:NO=INT(RND*40+10)
1050 FOR LL=1 TO NO:NN$=STR$(INT(RND*37))+" ":NN$=MID$(NN$,2,2):PRINT NN$;:FOR DL=0 TO 49:NEXT:PRINT CHR$(29);CHR$(29);:NEXT:PRINT W;"  "
1060 I=1
1070 IF A9(I)=W THEN GOTO 1100
1080 IF I<36 THEN I=I+1:GOTO 1070
1090 PRINT USING "YOU LOSE YOUR R#####.##";B:H=H+B:GOTO 1130
1100 B=FNA(O1*FNA(B)):C=C+B:H=H-B:PRINT "A WINNER WITH ";O1-1;" TO 1 ODDS!"
1110 PRINT USING "YOU WIN R#####.##";B
1120 IF H<=0 THEN GOTO 1170
1130 GOSUB 580
1140 INPUT "PLAY AGAIN";A$:IF A$="N" THEN GOTO 1160
1150 O1=2:FOR I=1 TO 36:A9(I)=37:NEXT:GOTO 830
1160 PRINT "AH WELL, SOME YOU WIN, SOME YOU LOSE.":GOTO 260
1170 PRINT "YOU HAVE BROKEN THE BANK,YOU PIG!"
1180 PRINT USING "YOU NOW HAVE R#####.##";C:END
1190 C=C+B:C=FNA(C)
1200 PRINT "YOU CAN'T BET WHAT YOU HAVEN'T GOT.":IF C>0 THEN GOTO 1130
1210 GOSUB 650:GOTO 1130
1220 PRINT "LOUSY BET. TRY AGAIN.":GOTO 1010
1230 PRINT "HIGH, LOW, ODDS, EVENS OR A DOZEN (H,L,O,E,D)":GOTO 870
1240 PRINT "INVALID INPUT":GOTO 970
1250 PRINT "IN ROULETTE, A RANDOM NUMBER BETWEEN 0 AND 36 IS CHOSEN."
1260 PRINT "YOU BET ON THE OUTCOME OF THIS CHOICE."
1270 PRINT "YOU MAY PLACE YOUR BET ON:"
1280 PRINT "(1) THE NUMBER ITSELF,"
1290 PRINT "      FOR WHICH THE BANK PAYS 35 TO 1"
1300 PRINT "    2 NUMBERS,"
1310 PRINT "      FOR WHICH THE BANK PAYS 17 TO 1"
1320 PRINT "    3 NUMBERS,"
1330 PRINT "      FOR WHICH THE BANK PAYS 11 TO 1"
1340 PRINT "    ETC., UP TO 36 NUMBERS,"
1350 PRINT "      WITH STEADILY DECREASING ODDS."
1360 PRINT "(2) HIGH (19-36), LOW (1-18),"
1370 PRINT "    ODDS (1,3,5,...35) OR EVENS (2,4,6...36)"
1380 PRINT "      FOR WHICH THE BANK PAYS EVEN MONEY."
1390 PRINT "(3) THE DOZEN (1-12, 13-24, 25-36)"
1400 PRINT "      FOR WHICH THE BANK PAYS 2 TO 1."
1410 PRINT " THE BANK NEVER PAYS ON 0 UNLESS YOU BET ON 0."
1420 PRINT USING "YOU NOW HAVE R#####.## TO PLAY WITH";C
1430 PRINT "GOOD LUCK!":GOTO 830
1440 REM ---------------------- SLOT MACHINE -------------------------------
1450 RESTORE:FOR I=1 TO 10:READ Z$(I):NEXT
1460 DATA APPLE,CHERRY,PEAR,PEACH,BANANA,ORANGE,GRAPE,LIBERTY,LIME,LEMON
1470 C9=C
1480 PRINT "THIS IS A SLOT MACHINE":PRINT "TO PULL THE HANDLE TYPE RETURN":PRINT "TO STOP TYPE 'S'"
1490 PRINT "YOU NOW HAVE ";INT(C);" SILVER RANDS"
1500 C=FNA(C)
1510 INPUT A$:IF A$="S" THEN GOTO 1730
1520 A=INT(RND*10):B=INT(RND*10):C1=INT(RND*10):C=C-1:H=H+1
1530 PRINT "<7><7><7><7><7><7><7>":PRINT Z$(A+1);" ";Z$(B+1);" ";Z$(C1+1)
1540 IF A<>B THEN GOTO 1600
1550 IF B<>C1 THEN GOTO 1610
1560 W=(A+1)*10
1570 IF A=7 THEN W=1000
1580 IF W>INT(H) THEN W=INT(H)
1590 GOTO 1640
1600 IF B<>C1 THEN GOTO 1650
1610 W=(A+1)*2
1620 IF A=7 THEN W=100
1630 GOTO 1580
1640 PRINT "YOU WIN R";W:GOTO 1710
1650 IF A<>1 THEN GOTO 1670
1660 W=1:GOTO 1580
1670 PRINT "YOU LOSE. YOU NOW HAVE R";INT(C)
1680 IF C<1 THEN GOTO 1700
1690 GOTO 1500
1700 PRINT "YOU HAVE NO MORE SILVER RANDS":GOTO 1730
1710 W=INT(W):C=C+W:H=H-W
1720 PRINT "YOU NOW HAVE R";INT(C):GOTO 1500
1730 IF ABS(C-C9)<.5 THEN PRINT "YOU SPLIT EVEN"
1740 IF C9-C>=.5 THEN PRINT USING "YOU LOST R#####.##";C9-C
1750 IF C-C9>=.5 THEN PRINT USING "YOU WON R#####.##";C-C9
1760 PRINT "YOU WERE A GREAT PLAYER!":PRINT "GOODBYE.":GOTO 260
1770 REM ----------------------- BLACKJACK ------------------------------
1780 PRINT "THIS IS THE GAME OF BLACKJACK OR VINGT-ET-UN."
1790 INPUT "DO YOU WANT INSTRUCTIONS";A$:IF A$="Y" THEN GOTO 2930
1800 PRINT "PLEASE WAIT WHILE I SHUFFLE THE CARDS..."
1810 FOR Q=1 TO 4:FOR P=1 TO 13:A(Q,P)=0:NEXT :NEXT
1820 J=5:K=5
1830 A8=0:T3=0
1840 FOR I=1 TO 17:A1(I)=0:GOSUB 2680:NEXT
1850 INPUT "WHAT IS YOUR BET";B:B=FNA(B):IF B<=0 THEN GOTO 2610
1860 C=C-B:IF C<0 THEN GOTO 2620
1870 PRINT "I WILL NOW DEAL EACH OF US 2 CARDS"
1880 IF D(2)+D(4)=21 THEN GOTO 2590
1890 IF A1(2)=1 AND D(4)=10 THEN GOTO 2590
1900 IF A1(4)=1 AND D(2)=10 THEN GOTO 2590
1910 H1=0
1920 INPUT "WHAT NOW";A$:A$=LEFT$(A$,1)
1930 IF A$="L" THEN GOTO 2010
1940 IF A$="Q" THEN GOTO 260
1950 IF A$="2" THEN GOTO 2050
1960 IF A$="H" THEN GOTO 2030
1970 IF A$="S" THEN GOTO 2160
1980 IF A$="B" THEN GOTO 2580
1990 IF A$="P" THEN GOTO 2020
2000 GOSUB 3140:GOTO 1920
2010 PRINT D$(4);" OF ";C$(4):GOTO 1920
2020 PRINT D$(1);" OF ";C$(1);" AND ";D$(3);" OF ";C$(3):GOTO 1920
2030 IF H1=1 THEN GOTO 1920
2040 PRINT D$(J);" OF ";C$(J):J=J+1:K=J:GOTO 1920
2050 IF J<>5 THEN GOTO 1920
2060 IF A1(1)<>1 THEN GOTO 2080
2070 T3=D(3)+11:GOTO 2110
2080 IF A1(3)<>1 THEN GOTO 2100
2090 T3=D(1)+11:GOTO 2110
2100 T3=D(1)+D(3)
2110 IF T3=21 THEN GOTO 2130
2120 D(1)=0:D(3)=0:GOTO 1920
2130 IF H1=1 THEN GOTO 2520
2140 PRINT "A NATURAL! YOU WIN R";:B=FNA(2.5*FNA(B)):PRINT B
2150 C=C+B:H=H-B:GOTO 2640
2160 T1=21
2170 IF H1=1 THEN GOTO 2400
2180 PRINT "I HAVE ";D$(2);" OF ";C$(2);" AND ";D$(4);" OF ";C$(4)
2190 A9=A1(2)+A1(4)
2200 T=D(2)+D(4)
2210 IF K=J THEN GOTO 2230
2220 FOR Q=K TO J-1:A9=A9+A1(Q):T=T+D(Q):NEXT
2230 IF A9=0 THEN T1=T:GOTO 2360
2240 T2=21-T
2250 GOSUB 2260:GOTO 2360
2260 T1=T+A9
2270 IF T2<11 THEN GOTO 2290
2280 IF A9=1 THEN T1=T+11
2290 IF T2<12 THEN GOTO 2310
2300 IF A9<3 THEN T1=T+10+A9
2310 IF T2<13 THEN GOTO 2330
2320 IF A9<4 THEN T1=T+10+A9
2330 IF T2<14 THEN GOTO 2350
2340 IF A9<5 THEN T1=T+10+A9
2350 RETURN
2360 IF T1<=16 THEN GOTO 2390 ELSE GOTO 2400
2370 PRINT "I BUST WITH ";T1;" - YOU WIN."
2380 C=C+2*B:H=H-B:GOTO 2640
2390 PRINT "I WILL DRAW A CARD.":PRINT "IT IS A ";D$(J);" OF ";C$(J):J=J+1:GOTO 2190
2400 A8=A1(1)+A1(3):T3=D(1)+D(3)
2410 IF K=5 THEN GOTO 2430
2420 FOR O=5 TO K-1:A8=A8+A1(O):T3=T3+D(O):NEXT
2430 IF A8=0 THEN GOTO 2470
2440 T2=21-T3:T=T3:T4=T1:A9=A8
2450 GOSUB 2260
2460 T3=T1:T1=T4
2470 IF T1>21 AND T3<=21 THEN GOTO 2370
2480 PRINT "YOUR TOTAL IS ";T3:IF T3>21 THEN GOTO 2550
2490 PRINT "MY TOTAL IS ";T1:IF T1>T3 THEN GOTO 2530
2500 IF T1=T3 THEN GOTO 2520
2510 PRINT "YOU WIN THIS TIME.":GOTO 2380
2520 PRINT "A DRAW - WE SPLIT THE POT.":C=C+B:GOTO 2640
2530 PRINT "YOU DIDN'T MAKE IT. YOU LOSE, SUCKER!"
2540 H=H+B:GOTO 2640
2550 PRINT "YOU MUST BUST ON A TOTAL BIGGER THAN 21":PRINT USING "YOU LOSE R#####.##";2*B
2560 C=C-B:IF C<0 THEN GOSUB 650
2570 GOTO 2540
2580 PRINT "YOU BUST AND LOSE.":GOTO 2540
2590 PRINT "I HAVE "D$(2);" OF ";C$(2);" AND ";D$(4);" OF ";C$(4):PRINT "WHICH MAKES 21. YOU MUST NOW STAND, UNLESS YOU CAN ALSO DECLARE A NATURAL.":PRINT "NO HITTING IS ALLOWED."
2600 H1=1:GOTO 1920
2610 PRINT "INVALID INPUT - TRY AGAIN...":GOTO 1850
2620 PRINT "YOU CAN'T BET WHAT YOU HAVEN'T GOT."
2630 C=C+B
2640 IF C<0 THEN GOSUB 650
2650 IF H<0 THEN GOTO 1170
2660 GOSUB 580
2670 INPUT "PLAY AGAIN";A$:C=FNA(C):IF A$="N" THEN GOTO 260 ELSE GOTO 1800
2680 GOSUB 2690:GOTO 2880
2690 E(I)=INT(4*RND)+1
2700 D(I)=INT(13*RND)+1
2710 R=E(I):S=D(I)
2720 IF A(R,S)=1 THEN GOTO 2690
2730 A(R,S)=1
2740 ON R GOTO 2750,2760,2770,2780
2750 R$=CHR$(3)+" HEARTS":GOTO 2790
2760 R$=CHR$(6)+" SPADES":GOTO 2790
2770 R$=CHR$(5)+" CLUBS":GOTO 2790
2780 R$=CHR$(4)+" DIAMONDS"
2790 ON S GOTO 2800,2810,2820,2820,2820,2820,2820,2820,2820,2830,2840,2850,2860
2800 S$="ACE":GOTO 2870
2810 S$="DEUCE":GOTO 2870
2820 D1$="123456789":S$=MID$(D1$,S,1):GOTO 2870
2830 S$="10":GOTO 2870
2840 S$="JACK":GOTO 2870
2850 S$="QUEEN":GOTO 2870
2860 S$="KING"
2870 C$(I)=R$:D$(I)=S$:RETURN
2880 IF D(I)=1 THEN A1(I)=1
2890 IF D(I)=1 THEN D(I)=0
2900 IF D(I)>=11 THEN D(I)=10
2910 RETURN
2920 PRINT "THE GAME OF 21 IS PLAYED WITH A FULL DECK OF CARDS."
2930 PRINT "THE CARDS HAVE NUMERICAL VALUES AS FOLLOWS:"
2940 PRINT "1.  AN ACE COUNTS 1 OR 11 (YOUR CHOICE ),"
2950 PRINT "2.  A FACE CARD (JACK,QUEEN OR KING ) COUNTS 10,"
2960 PRINT "3.  ANY OTHER CARD COUNTS ITS INDEX NUMBER."
2970 PRINT "THE CROUPIER DEALS TWO CARDS TO YOU ( BOTH FACE DOWN )"
2980 PRINT "AND TWO TO HERSELF (ONE FACE DOWN, ONE FACE UP )."
2990 PRINT "YOU PLACE YOUR BET BEFORE THE DEAL."
3000 PRINT "   THE OBJECT OF THE GAME IS TO GET A CARD TOTAL OF 21"
3010 PRINT "OR AS CLOSE AS POSSIBLE WITHOUT GOING OVER 21."
3020 PRINT "TO DO THIS YOU MAY DRAW ADDITIONAL CARDS."
3030 PRINT "IF YOUR FIRST 2 CARDS TOTAL 21, YOU HAVE A 'NATURAL' AND"
3040 PRINT "WIN 1.5 TIMES YOUR BET..."
3050 PRINT "UNLESS THE CROUPIER ALSO HAS A NATURAL."
3060 PRINT "IF THIS HAPPENS, YOUR BET IS RETURNED."
3070 PRINT "   IF YOU HAVE A TOTAL OF OVER 21,"
3080 PRINT "YOU MUST 'BUST' OR ELSE YOU LOSE TWICE YOUR BET."
3090 INPUT "DO YOU WANT TO HEAR MORE";A$:IF A$="N" THEN GOTO 3220
3100 PRINT "THE FOLLOWING QUESTION WILL APPEAR REPEATEDLY..."
3110 PRINT "   WHAT NOW?"
3120 PRINT "YOU THEN HAVE SEVERAL OPTIONS:"
3130 GOSUB 3140:GOTO 3220
3140 PRINT "ENTER ONE OF THE FOLLOWING INSTRUCTIONS..."
3150 PRINT "'P' - PEEK      TO LOOK AT YOUR FACE DOWN-CARDS"
3160 PRINT "'H' - HIT          DRAW ADDITIONAL CARDS"
3170 PRINT "'2' - 21           DECLARE THAT YOU HAVE A NATURAL"
3180 PRINT "'S' - STAND        STOP DRAWING ADDITIONAL CARDS"
3190 PRINT "'B' - BUST         DECLARE A TOTAL OF OVER 21"
3200 PRINT "'L' - LOOK         LOOK AT THE CROUPIER'S FACE-UP CARD"
3210 RETURN
3220 PRINT USING "YOU NOW HAVE R#####.##";C:PRINT "I HOPE YOU WIN SOMETHING.":GOTO 1800
3230 REM ----------------------- REDDOG ---------------------------------
3240 PRINT "THIS IS THE GAME OF REDDOG OR HIGH CARD POOL"
3250 INPUT "DO YOU NEED INSTRUCTIONS";A$:IF A$="Y" THEN GOTO 3960
3260 N=INT(50*RND)+2:PRINT "THE POOL IS R";N;" EACH"
3270 C=C-N:H=H-N:P1=0
3280 P=P+N+N
3290 IF C<0 THEN GOSUB 650
3300 IF H<0 THEN GOTO 1170
3310 IF P1=1 THEN GOTO 3550
3320 B1=1:B2=1:B3=1:B4=1
3330 PRINT "I WILL DEAL"
3340 FOR Q=1 TO 4:FOR R=1 TO 13:A(Q,R)=0:NEXT :NEXT
3350 FOR I=1 TO 12:GOSUB 4120:NEXT
3360 FOR I=1 TO 9 STEP 2:PRINT "YOUR #";(I+1)/2;" CARD IS ";D$(I);" OF ";C$(I):NEXT
3370 INPUT "WHAT IS YOUR BET";B:B=FNA(B)
3380 IF B<1 THEN GOTO 3880
3390 IF B>P THEN GOTO 3520
3400 C9=C-B:IF C9<0 THEN GOTO 3950
3410 PRINT "THE TOP CARD OF THE STOCK IS ";D$(11);" OF ";C$(11)
3420 C=C-B
3430 INPUT "CAN YOU BEAT IT";A$:IF A$="N" THEN GOTO 3530
3440 INPUT "WITH WHAT NUMBER CARD";X:IF X<1 OR X>5 THEN GOTO 3440
3450 X=X+X-1
3460 IF E(11)<>E(X) THEN GOTO 3430
3470 IF D(11)>D(X) THEN GOTO 3430
3480 PRINT USING "YOU WIN R#####.##";B+B
3490 C=C+B+B:P=P-B:P1=1
3500 IF P<1 THEN GOTO 3850
3510 GOTO 3550
3520 PRINT "THE POT IS R";P;" YOU CAN'T BET OVER THIS":GOTO 3370
3530 PRINT USING "YOUR R#####.## GOES TO THE POT";B
3540 P=P+B
3550 FOR I=2 TO 10 STEP 2
3560 ON E(I) GOTO 3570,3590,3610,3630
3570 IF B1<D(I) THEN B1=D(I)
3580 GOTO 3640
3590 IF B2<D(I) THEN B2=D(I)
3600 GOTO 3640
3610 IF B3<D(I) THEN B3=D(I)
3620 GOTO 3640
3630 IF B4<D(I) THEN B4=D(I)
3640 NEXT
3650 B=INT(P*(((14-B1)+(14-B2)+(14-B3)+(14-B4)-3)/44))
3660 IF B>H THEN B=H
3670 IF B<1 THEN B=1
3680 IF B>P THEN B=P
3690 H=H-B:IF H<0 THEN GOTO 1170
3700 PRINT USING "MY BET IS R#####.##";B
3710 PRINT "THE NEXT TOP CARD IS THE ";D$(12);" OF ";C$(12)
3720 I=2
3730 IF E(I)<>E(12) THEN GOTO 3760
3740 IF D(I)<D(12) THEN GOTO 3760
3750 GOTO 3830
3760 IF I<10 THEN I=I+2:GOTO 3730
3770 PRINT "I LOSE"
3780 PRINT "MY CARDS WERE:"
3790 P=P+B
3800 FOR I=2 TO 10 STEP 2:PRINT "THE ";D$(I);" OF ";C$(I):NEXT
3810 PRINT USING "THE POT NOW HOLDS R#####.##";P
3820 GOSUB 580:GOTO 3910
3830 PRINT "I WIN WITH A ";D$(I);" OF ";C$(I):H=H+B+B:P=P-B:P1=2:IF P<1 THEN GOTO 3850
3840 P1=0:GOTO 3810
3850 PRINT "IN ORDER TO PLAY YOU MUST ANTE R";N
3860 GOSUB 580
3870 IF N>C THEN GOTO 3890 ELSE GOTO 3910
3880 PRINT "YOU HAVE TO BET AT LEAST R1.00":GOTO 3370
3890 PRINT "WHICH IS NOT ENOUGH TO PLAY THIS GAME"
3900 PRINT "SO LONG":GOTO 260
3910 INPUT "PLAY AGAIN";A$:C=FNA(C):IF A$="N" THEN GOTO 3940
3920 IF P1<>0 THEN GOTO 3270
3930 GOTO 3320
3940 C=C+P/2:H=H+P/2:PRINT "GOODBYE":GOTO 260
3950 PRINT "YOU CAN'T BET WHAT YOU HAVEN'T GOT":GOTO 3860
3960 PRINT "IN REDDOG A NUMBER OF RANDS ARE PLACED INTO A POT"
3970 PRINT "WHICH IS COMMON PROPERTY BETWEEN US"
3980 PRINT "AT THE END OF THE GAME YOUR SHARE IS RETURNED TO YOU"
3990 PRINT "AFTER I DEAL BOTH OF US 5 CARDS EACH, YOU MAY BET."
4000 PRINT "THE OBJECT OF PLAY IS TO BEAT THE CARD"
4010 PRINT "ON TOP OF THE STOCK."
4020 PRINT "TO BEAT IT ONE MUST HAVE A CARD OF THE SAME"
4030 PRINT "SUIT BUT OF A HIGHER RANK (ACE IS HIGH)."
4040 PRINT "YOU MUST BET AT LEAST R1.00 AND YOU CANNOT BET"
4050 PRINT "OVER THE AMOUNT OF MONEY IN THE POT"
4060 PRINT "IF YOU WIN, YOU GET THE AMOUNT YOU BET PLUS THE"
4070 PRINT "SAME AMOUNT FROM THE POT."
4080 PRINT "IF YOU LOSE, THE MONEY YOU BET GOES INTO THE POT."
4090 PRINT "IF I RUN OUT OF MONEY, YOU KEEP THE WHOLE POT"
4100 PRINT USING "YOU NOW HAVE R#####.##";C
4110 PRINT "GOOD LUCK!":INPUT "HIT ANY KEY TO BEGIN";A$:GOTO 3260
4120 GOSUB 2690:IF D(I)=1 THEN D(I)=14
4130 RETURN
4140 REM ----------------------- POKER ----------------------------------
4150 PRINT "WELCOME TO THE POKER TABLE.":GOSUB 580:PRINT "THE ANTE IS R5.00":PRINT "TO CHECK BET R1, TO FOLD BET R0."
4160 PRINT "ENOUGH TALK! LET'S GET DOWN TO BUSINESS!"
4170 P7=1
4180 C=C-5:H=H-5:B=0:L=0:L1=0:B2=0:C7=0:P1=0:F2=10
4190 IF C<=2 THEN GOSUB 650
4200 IF C<0 THEN GOTO 5490
4210 IF H0 THEN GOTO 4740
4220 PRINT "I WILL DEAL"
4230 FOR Q=1 TO 4:FOR O=1 TO 13
4240 A(Q,O)=0
4250 NEXT :NEXT
4260 FOR I=1 TO 20:GOSUB 4120:NEXT
4270 PRINT "YOUR HAND..."
4280 FOR I=1 TO 5:PRINT I;"- ";D$(2*I-1);" OF ";C$(2*I-1):NEXT
4290 INPUT "WHAT'S YOUR BET";B:B=FNA(B):C=C-B
4300 IF B<=0 THEN GOSUB 4690:GOTO 5470
4310 IF C<0 THEN GOTO 4700
4320 F2=10+B:GOSUB 4350:GOSUB 4510
4330 IF F1=1 THEN GOTO 5470
4340 GOTO 4750
4350 REM **** SEE IF ACE HIGH OR LOW WINS IN MY HAND
4360 J1=2:GOSUB 5570:GOSUB 5640
4370 IF D(2)=14 THEN GOTO 4390 ELSE GOTO 4460
4380 F2=B+10
4390 D(2)=1:Q1=P(2)
4400 FOR I=0 TO 4:Y(I)=W1(I,2):NEXT
4410 GOSUB 5570:GOSUB 5640:P(1)=Q1
4420 FOR I=0 TO 4:W1(I,1)=Y(I):NEXT
4430 GOSUB 6250
4440 IF J1=1 THEN D(10)=14
4450 J1=2:GOSUB 5570:GOSUB 5640
4460 B1=(P(2)+W1(0,2)/20)*(H/50)
4470 B1=INT(B1*P7)
4480 IF B1<1 THEN B1=1
4490 IF B1>C AND RND>.1 THEN B1=INT(C)
4500 RETURN
4510 REM ****** CALCULATE BET
4520 IF B1<B THEN GOTO 4590
4530 IF (B1-B)/B>.1 THEN GOTO 4570
4540 F2=10+B+B
4550 B1=B:PRINT "I SEE YOU."
4560 F1=0:RETURN
4570 PRINT USING "I SEE YOU AND RAISE YOU R#####.##";B1-B
4580 F2=10+B+B1:GOTO 4610
4590 IF RND<P7 THEN GOTO 4550
4600 PRINT "I FOLD. YOU WIN THIS ONE.":C=C+F2:P7=P7/1.1:F1=1:RETURN
4610 INPUT "WHAT'S YOUR BET";L:L=FNA(L):C=C-L
4620 IF C<0 THEN GOTO 5500
4630 IF L<=0 THEN GOTO 4690
4640 B=B+L
4650 IF B<B1 THEN GOTO 4680
4660 F2=F2+L
4670 IF B1=B THEN GOTO 4560 ELSE GOTO 4510
4680 PRINT "EITHER SEE ME OR FOLD, DAMMIT!":C=C+L:B=B-L:GOTO 4610
4690 PRINT "YOU FOLD. I WIN THIS ONE.":H=H+F2:P7=P7*1.1:F1=1:RETURN
4700 PRINT "YOU CAN'T BET WHAT YOU HAVEN'T GOT!":C=C+B
4710 IF C<=0 THEN GOTO 680
4720 GOSUB 580
4730 GOTO 4290
4740 PRINT "I'M BUSTED. CONGRATULATIONS.":END
4750 H=H-B1
4760 IF H<=0 THEN GOTO 4740
4770 PRINT "NOW WE DRAW."
4780 INPUT "HOW MANY CARDS DO YOU WANT";F
4790 IF F>5 OR F<0 THEN GOTO 4780
4800 IF F=0 THEN GOTO 4950
4810 IF F=1 THEN GOTO 4840
4820 IF F=5 THEN GOTO 4850
4830 PRINT "WHAT ARE THEIR NUMBERS (ONE AT A TIME)":GOTO 4850
4840 PRINT "WHAT IS IT'S NUMBER?"
4850 FOR Q=1 TO F
4860 IF F=5 THEN G=Q:GOTO 4890
4870 INPUT G
4880 IF G>5 OR G<0 THEN GOTO 5530
4890 G=G*2-1
4900 D(G)=D(Q+10):E(G)=E(Q+10)
4910 C$(G)=C$(Q+10):D$(G)=D$(Q+10)
4920 NEXT
4930 PRINT "YOUR NEW HAND...."
4940 FOR I=1 TO 5:PRINT I;"- ";D$(2*I-1);" OF ";C$(2*I-1):NEXT
4950 IF P(2)>4 THEN F1=0
4960 IF P(2)=4 THEN F1=2
4970 IF P(2)=3 THEN F1=1
4980 IF P(2)<=2 THEN F1=3
4990 PRINT "I DRAW ";F1:IF F1=0 THEN GOTO 5040
5000 FOR Q=1 TO F1:O=K(Q)
5010 D(O)=D(Q+10+F):E(O)=E(Q+10+F)
5020 C$(O)=C$(Q+10+F):D$(O)=D$(Q+10+F)
5030 NEXT
5040 INPUT "WHAT'S YOUR BET";B2:B2=FNA(B2)
5050 C=C-B2
5060 IF B2<=0 THEN GOSUB 4690:GOTO 5470
5070 IF C<0 THEN GOTO 5540
5080 B=B2+B
5090 IF B2<=0 THEN GOTO 4690
5100 B3=B1
5110 F2=F2+B2
5120 GOSUB 4350
5130 B1=B1+B3
5140 GOSUB 4510
5150 IF F1=1 THEN GOTO 5470
5160 PRINT "NOW WE COMPARE HANDS..."
5170 PRINT "MY HAND:":FOR Q=2 TO 10 STEP 2:PRINT Q/2;"- ";D$(Q);" OF ";C$(Q):NEXT
5180 REM **** SEE IF ACE HIGH OR LOW WINS IN HUMANS MAND
5190 J1=1:GOSUB 5570:GOSUB 5640
5200 IF D(1)=14 THEN GOTO 5220
5210 GOTO 5380
5220 D(1)=1
5230 Q1=P(2)
5240 FOR I=0 TO 4:Y(I)=W1(I,2):NEXT
5250 Q2=P(1)
5260 FOR I=0 TO 4:Y1(I)=W1(I,1):NEXT
5270 GOSUB 5570
5280 GOSUB 5640
5290 P(2)=Q2
5300 FOR I=0 TO 4:W1(I,2)=Y1(I):NEXT
5310 GOSUB 6250
5320 IF J1=2 THEN D(9)=14
5330 J1=1
5340 P(2)=Q1
5350 FOR I=0 TO 4:W1(I,2)=Y(I):NEXT
5360 GOSUB 5570
5370 GOSUB 5640
5380 PRINT "YOU HAVE ";P$(1);" ";W$(1):PRINT "AND I HAVE ";P$(2);" ";W$(2)
5390 P2=5+B
5400 P1=P2+P2
5410 GOSUB 6250
5420 IF J1=1 THEN GOTO 5450
5430 IF J1=0 THEN GOTO 5460
5440 PRINT "TOUGH LUCK, SUCKER! YOU LOSE.":H=H+P1:P7=P7*1.5:GOTO 5470
5450 PRINT "IT IS MY PLEASURE TO ANNOUNCE THAT YOU WIN.":C=C+P1:P7=P7/1.3:GOTO 5470
5460 PRINT "IT'S A DRAW. WE SPLIT THE POT.":C=C+P2:H=H+P2
5470 GOSUB 580:INPUT "PLAY AGAIN";A$:C=FNA(C):IF A$="Y" THEN GOTO 4180
5480 PRINT "SORRY TO SEE YOU GO. GOODBYE!":GOTO 260
5490 C=C+1:N=C+1:GOTO 3860
5500 PRINT "YOU CAN'T BET WHAT YOU HAVEN'T GOT.":C=C+L:C=FNA(C)
5510 IF C<=0 THEN GOSUB 650
5520 GOSUB 580:GOTO 4610
5530 PRINT "INVALID INPUT. TRY AGAIN.":GOTO 4830
5540 PRINT "YOU CAN'T BET WHAT YOU HAVEN'T GOT.":C=C+B2:C=FNA(C)
5550 IF C<=0 THEN GOSUB 650
5560 GOSUB 580:GOTO 5040
5570 FOR I=1 TO 4:FOR J=J1 TO J1+7 STEP 2
5580 IF D(J)>=D(J+2) THEN GOTO 5630
5590 Z1=D(J):D(J)=D(J+2):D(J+2)=Z1
5600 Z1=E(J):E(J)=E(J+2):E(J+2)=Z1
5610 S$=D$(J):D$(J)=D$(J+2):D$(J+2)=S$
5620 R$=C$(J):C$(J)=C$(J+2):C$(J+2)=R$
5630 NEXT :NEXT :RETURN
5640 J2=J1+2:J3=J2+2:J4=J3+2:J5=J4+2
5650 FOR O=0 TO 4:W(O,J1)=0:NEXT
5660 IF E(J1)<>E(J2) OR E(J2)<>E(J3) THEN GOTO 5720
5670 IF E(J3)<>E(J4) OR E(J4)<>E(J5) THEN GOTO 5720
5680 IF D(J1)<>D(J2)+1 OR D(J2)<>D(J3)+1 THEN GOTO 5720
5690 IF D(J3)<>D(J4)+1 OR D(J4)<>D(J5)+1 THEN GOTO 5720
5700 P(J1)=9
5710 P$(J1)="STRAIGHT FLUSH":W(0,J1)=J1:GOTO 6200
5720 IF D(J3)<>D(J4) OR D(J2)<>D(J3) THEN GOTO 5770
5730 IF D(J1)<>D(J2) THEN GOTO 5750
5740 P(J1)=8:P$(J1)="FOUR OF A KIND":W(0,J1)=J2:W(1,J1)=J1:GOTO 6200
5750 IF D(J4)<>D(J5) THEN GOTO 5770
5760 P(J1)=8:P$(J1)="FOUR OF A KIND":W(0,J1)=J2:W(1,J2)=J1:GOTO 6200
5770 IF D(J1)<>D(J2) OR D(J4)<>D(J5) THEN GOTO 5820
5780 IF D(J2)<>D(J3) THEN GOTO 5800
5790 P(J1)=7:P$(J1)="FULL HOUSE":W(0,J1)=J3:W(1,J1)=J4:GOTO 6200
5800 IF D(J3)<>D(J4) THEN GOTO 5820
5810 P(J1)=7:W(0,J1)=J3:W(1,J1)=J2:P$(J1)="FULL HOUSE":GOTO 6200
5820 IF E(J1)<>E(J2) OR E(J2)<>E(J3) THEN GOTO 5850
5830 IF E(J3)<>E(J) OR E(J4)<>E(J5) THEN GOTO 5850
5840 P(J1)=6:P$(J1)="FLUSH":FOR Q=0 TO 4:W(Q,J1)=J1+Q*2:NEXT :GOTO 6200
5850 IF D(J1)<>D(J2)+1 OR D(J2)<>D(J3)+1 THEN GOTO 5880
5860 IF D(J3)<>D(J4)+1 OR D(J4)<>D(J5)+1 THEN GOTO 5880
5870 P(J1)=5:W(0,J1)=J1:P$(J1)="STRAIGHT":GOTO 6200
5880 IF D(J2)<>D(J3) THEN GOTO 5950
5890 IF D(J1)<>D(J2) THEN GOTO 5930
5900 P(J1)=4:W(0,J1)=J3:W(1,J1)=J4:W(2,J1)=J5:K(1)=J4
5910 K(2)=J5
5920 P$(J1)="THREE OF A KIND":GOTO 6200
5930 IF D(J3)<>D(J4) THEN GOTO 5980
5940 P(J1)=4:W(0,J1)=J3:W(1,J1)=J1:W(2,J1)=J5:K(1)=J1:GOTO 5910
5950 IF D(J3)<>D(J4) THEN GOTO 5980
5960 IF D(J4)<>D(J5) THEN GOTO 5980
5970 P(J1)=4:W(0,J1)=J3:W(1,J1)=J1:W(2,J1)=J2:K(1)=J1:K(2)=J2:GOTO 5920
5980 IF D(J1)<>D(J2) THEN GOTO 6040
5990 IF D(J3)<>D(J4) THEN GOTO 6020
6000 P(J1)=3:W(0,J1)=J1:W(1,J1)=J3:W(2,J1)=J5:K(1)=J5
6010 P$(J1)="TWO PAIRS":GOTO 6200
6020 IF D(J4)<>D(J5) THEN GOTO 6070
6030 P(J1)=3:W(0,J1)=J1:W(1,J1)=J4:W(2,J1)=J3:K(1)=J3:GOTO 6010
6040 IF D(J2)<>D(J3) THEN GOTO 6070
6050 IF D(J4)<>D(J5) THEN GOTO 6070
6060 P(J1)=3:W(0,J1)=J2:W(1,J1)=J4:W(2,J1)=J1:K(1)=J1:GOTO 6010
6070 IF D(J1)<>D(J2) THEN GOTO 6120
6080 P(J1)=2:W(0,J1)=J1:W(1,J1)=J3:W(2,J1)=J4:W(3,J1)=J5:K(1)=J3
6090 K(2)=J4
6100 K(3)=J5
6110 P$(J1)="ONE PAIR":GOTO 6200
6120 IF D(J2)<>D(J3) THEN GOTO 6140
6130 P(J1)=2:W(0,J1)=J2:W(1,J1)=J1:W(2,J1)=J4:W(3,J1)=J5:K(1)=J1:GOTO 6090
6140 IF D(J3)<>D(J4) THEN GOTO 6160
6150 P(J1)=2:W(0,J1)=J3:W(1,J1)=J1:W(2,J1)=J2:W(3,J1)=J5:K(1)=J1:K(2)=J2:GOTO 6100
6160 IF D(J4)<>D(J5) THEN GOTO 6180
6170 P(J1)=2:W(0,J1)=J4:W(1,J1)=J1:W(2,J1)=J2:W(3,J1)=J3:K(1)=J1:K(2)=J2:K(3)=J3:GOTO 6110
6180 P(J1)=1
6190 FOR Q=0 TO 4:W(Q,J1)=J1+Q*2:K(Q+1)=J1+Q*2:NEXT :P$(J1)="SCHMALTZ"
6200 FOR Q=0 TO 4
6210 IF W(Q,J1)<>0 THEN W1(Q,J1)=D(W(Q,J1))
6220 IF W(Q,J1)=0 THEN W1(Q,J1)=0
6230 NEXT
6240 W$(J1)=D$(W(0,J1)):RETURN
6250 IF P(1)>P(2) THEN GOTO 6310
6260 IF P(1)<P(2) THEN GOTO 6320
6270 FOR Q=0 TO 4
6280 IF W1(Q,1)>W1(Q,2) THEN GOTO 6310
6290 IF W1(Q,1)<W1(Q,2) THEN GOTO 6320
6300 NEXT :J1=0:RETURN
6310 J1=1:RETURN
6320 J1=2:RETURN

10 RANDOMIZE TIMER
20 PRINT TAB(33);"POKER":PRINT TAB(15);"(c) CREATIVE COMPUTING":PRINT :PRINT :PRINT :DIM A(50),B(15)
30 DIM TEN(5):TEN(0)=1:FOR I=1 TO 5:TEN(I)=10*TEN(I-1):NEXT
40 DEF FNA(X)=INT(10*RND)
50 DEF FNB(X)=X-100*INT(X/100)
60 PRINT "WELCOME TO THE CASINO. WE EACH HAVE $200"
70 PRINT "I WILL OPEN THE BETTING AFTER THE DRAW; YOU OPEN AFTER"
80 PRINT "WHEN YOU FOLD, BET 0: TO CHECK, BET 0.5"
90 PRINT "ENOUGH TALK --- LET'S GET DOWN TO BUSINESS"
100 PRINT
110 O=1
120 C=200
130 S=200
140 P=0
150 PRINT
160 IF C=5 THEN GOTO 1960
170 PRINT "THE ANTE IS $5. I WILL DEAL"
180 PRINT
190 IF S>5 THEN GOTO 210
200 GOSUB 2040
210 P=P+10
220 S=S-5
230 C=C-5
240 FOR Z=1 TO 10:GOSUB 950:NEXT
250 PRINT "YOUR HAND:":N=1:GOSUB 1040:N=6:I=2:GOSUB 1200:PRINT
260 IF I<>6 THEN GOTO 350
270 IF FNA(0)<=7 THEN GOTO 290
280 X=11100:GOTO 330
290 IF FNA(0)<=7 THEN GOTO 310
300 X=11100:GOTO 330
310 IF FNA(0)>=1 THEN GOTO 340
320 X=11111
330 I=7:Z=23:GOTO 430
340 Z=1:GOTO 380
350 IF U>=13 THEN GOTO 390
360 IF FNA(0)>=2 THEN GOTO 370 ELSE GOTO 330
370 Z=0
380 K=0:PRINT "I CHECK":GOTO 440
390 IF U<=16 THEN GOTO 420
400 Z=2
410 IF FNA(0)>=1 THEN GOTO 430
420 Z=35
430 V=Z+FNA(0):GOSUB 1850:PRINT "I'LL OPEN WITH ";V:K=V
440 GOSUB 1590:GOSUB 450:GOTO 510
450 IF I<>3 THEN GOTO 480 ELSE PRINT
460 PRINT "I WIN":C=C+P
470 PRINT "I NOW HAVE $";C;" AND YOU HAVE $";S:INPUT "DO YOU WANT TO PLAY AGAIN";H$:IF LEFT$(H$,1)="Y" THEN GOTO 140 ELSE END
480 IF I<>4 THEN GOTO 500 ELSE PRINT
490 PRINT "YOU WIN":S=S+P:GOTO 470
500 RETURN
510 PRINT :PRINT "NOW WE DRAW -- HOW MANY CARDS DO YOU WANT";
520 INPUT T:IF T=0 THEN GOTO 570
530 Z=10:IF T<4 THEN GOTO 550
540 PRINT "YOU CAN'T DRAW MORE THAN THREE CARDS":GOTO 520
550 PRINT "WHAT ARE THEIR NUMBERS":FOR Q=1 TO T:INPUT U:GOSUB 940:NEXT Q
560 PRINT "YOUR NEW HAND:":N=1:GOSUB 1040
570 Z=10+T:FOR U=6 TO 10:IF INT(X/TEN(U-6))=10*INT(X/TEN(U-5)) THEN GOSUB 940
580 NEXT :PRINT :PRINT "I AM TAKING ";Z-10-T;" CARD";
590 IF Z=11+T THEN GOTO 610
600 PRINT "S":PRINT :GOTO 620
610 PRINT
620 N=6:V=I:I=1:GOSUB 1200:B=U:M=D:IF V<>7 THEN GOTO 640
630 Z=28:GOTO 750
640 IF I<>6 THEN GOTO 660
650 Z=1:GOTO 750
660 IF U>=13 THEN GOTO 700
670 Z=2:IF FNA(0)<>6 THEN GOTO 690
680 Z=19
690 GOTO 750
700 IF U>=16 THEN GOTO 740
710 Z=19:IF FNA(0)<>8 THEN GOTO 730
720 Z=11
730 GOTO 750
740 Z=2
750 K=0:GOSUB 1590
760 IF T<>.5 THEN GOTO 820
770 IF V=7 THEN GOTO 800
780 IF I<>6 THEN GOTO 800
790 PRINT "I'LL CHECK":GOTO 830
800 V=Z+FNA(0):GOTO 1850
810 PRINT "I'LL BET $";V:K=V:GOSUB 1600
820 GOSUB 450
830 PRINT :PRINT "NOW WE COMPARE HANDS":J$=H$:K$=I$
840 PRINT "MY HAND:":N=6:GOSUB 1040:N=1:GOSUB 1200:PRINT
850 PRINT "YOU HAVE ";:K=D:GOSUB 1970:H$=J$:I$=K$:K=M:PRINT "AND I HAVE ";
860 GOSUB 1970:IF B>U THEN GOTO 460
870 IF U>B THEN GOTO 490
880 IF H$="A FLUS" THEN GOTO 920
890 IF FNB(M)<FNB(D) THEN GOTO 490
900 IF FNB(M)>FNB(D) THEN GOTO 460
910 PRINT "THE HAND IS DRAWN":PRINT "ALL $";P;" REMAINS IN THE POT":GOTO 150
920 IF FNB(M)>FNB(D) THEN GOTO 460
930 IF FNB(D)>FNB(M) THEN GOTO 490 ELSE GOTO 910
940 Z=Z+1
950 A(Z)=100*INT(4*RND)+INT(100*RND)
960 IF INT(A(Z)/100)>3 THEN GOTO 950
970 IF A(Z)-100*INT(A(Z)/100)>12 THEN GOTO 950
980 IF Z=1 THEN GOTO 1030
990 FOR K=1 TO Z-1:IF A(Z)=A(K) THEN GOTO 950
1000 NEXT
1010 IF Z<=10 THEN GOTO 1030
1020 N=A(U):A(U)=A(Z):A(Z)=N
1030 RETURN
1040 FOR Z=N TO N+4
1050 PRINT Z;"--  ";:GOSUB 1070:PRINT " OF ";:GOSUB 1140
1060 NEXT :PRINT :RETURN
1070 K=FNB(A(Z))
1080 IF K=9 THEN PRINT "JACK";
1090 IF K=10 THEN PRINT "QUEEN";
1100 IF K=11 THEN PRINT "KING";
1110 IF K=12 THEN PRINT "ACE";
1120 IF K<9 THEN PRINT K+2;
1130 RETURN
1140 K=INT(A(Z)/100)
1150 IF K=0 THEN PRINT "CLUBS"
1160 IF K=1 THEN PRINT "DIAMONDS"
1170 IF K=2 THEN PRINT "HEARTS"
1180 IF K=3 THEN PRINT "SPADES"
1190 RETURN
1200 U=0:FOR Z=N TO N+4
1210 B(Z)=FNB(A(Z)):IF Z=N+4 THEN GOTO 1240
1220 IF INT(A(Z)/100)<>INT(A(Z+1)/100) THEN GOTO 1240
1230 U=U+1
1240 NEXT :IF U<>4 THEN GOTO 1260
1250 X=11111:D=A(N):H$="A FLUS":I$="H IN ":U=15:RETURN
1260 FOR Z=N TO N+3:FOR K=Z+1 TO N+4:IF B(Z)<=B(K) THEN GOTO 1280
1270 X=A(Z):A(Z)=A(K):B(Z)=B(K):A(K)=X:B(K)=FNB(A(K))
1280 NEXT :NEXT :X=0
1290 FOR Z=N TO N+3
1300 IF B(Z)<>B(Z+1) THEN GOTO 1330
1310 IF Z=N THEN X=X+11*TEN(Z-N)
1320 D=A(Z):GOSUB 1480
1330 NEXT :IF X<>0 THEN GOTO 1400
1340 IF B(N)+3<>B(N+3) THEN GOTO 1360
1350 X=1111:U=10
1360 IF B(N+1)+3<>B(N+4)THEN GOTO 1400
1370 IF U<>10 THEN GOTO 1390
1380 U=14:H$="STRAIG":I$="HT":X=11111:D=A(N+4):RETURN
1390 U=10:X=11110
1400 IF U>=10 THEN GOTO 1420
1410 D=A(N+4):H$="SCHMAL":I$="TZ, ":U=9:X=11000:GOTO 1460
1420 IF U<>10 THEN GOTO 1440
1430 IF I=1 THEN GOTO 1460 ELSE GOTO 1470
1440 IF U>12 THEN GOTO 1470
1450 IF FNB(D)>6 THEN GOTO 1470
1460 I=6
1470 RETURN
1480 IF U>=11 THEN GOTO 1500
1490 U=11:H$="A PAIR":I$=" OF ":RETURN
1500 IF U<>11 THEN GOTO 1540
1510 IF B(Z)<>B(Z-1) THEN GOTO 1530
1520 H$="THREE":I$=" ":U=13:RETURN
1530 H$="TWO P":I$="AIRS, ":U=12:RETURN
1540 IF U>12 THEN GOTO 1560
1550 U=16:H$="FULL H":I$="OUSE, ":RETURN
1560 IF B(Z)<>B(Z-1) THEN GOTO 1580
1570 U=17:H$="FOUR":I$=" ":RETURN
1580 U=16:H$="FULL H":I$="OUSE, ":RETURN
1590 G=0
1600 INPUT "WHAT IS YOUR BET";T:IF T-INT(T)=0 THEN GOTO 1650
1610 IF K<>0 THEN GOTO 1640
1620 IF G<>0 THEN GOTO 1640
1630 IF T=.5 THEN GOTO 1820
1640 PRINT "NO SMALL CHANGE, PLEASE":GOTO 1600
1650 IF S-G-T>=0 THEN GOTO 1670
1660 GOSUB 2040:GOTO 1600
1670 IF T<>0 THEN GOTO 1690
1680 I=3:GOTO 1810
1690 IF G+T>=K THEN GOTO 1710
1700 PRINT "IF YOU CAN'T SEE MY BET, THEN FOLD":GOTO 1600
1710 G=G+T:IF G=K THEN GOTO 1810
1720 IF Z<>1 THEN GOTO 1830
1730 IF G>5 THEN GOTO 1760
1740 IF Z>=2 THEN GOTO 1790
1750 V=5:GOTO 1830
1760 IF Z=1 THEN GOTO 1780
1770 IF T<=25 THEN GOTO 1790
1780 I=4:PRINT "I FOLD":RETURN
1790 IF Z=2 THEN GOTO 1840
1800 PRINT "I'LL SEE YOU":K=G
1810 S=S-G:C=C-K:P=P+G+K
1820 RETURN
1830 IF G>3*Z THEN GOTO 1790
1840 V=G-K+FNA(0):GOSUB 1850:PRINT "I'LL SEE YOU AND RAISE YOU $";V:K=G+V:GOTO 1600
1850 IF C-G-V>=0 THEN GOTO 1950
1860 IF G<>0 THEN GOTO 1880
1870 V=C:RETURN
1880 IF C-G>=0 THEN GOTO 1800
1890 IF (O/2)<>INT(O/2) THEN GOTO 1920
1900  INPUT "WOULD YOU LIKE TO BUY BACK YOUR WATCH FOR $50";J$:IF LEFT$(J$,1)="N" THEN GOTO 1920
1910 C=C+50:O=O/2:RETURN
1920 IF O/3<>INT(O/3) THEN GOTO 1960
1930 INPUT "WOULD YOU LIKE TO BUY BACK YOUR TIE PIN FOR $50";J$:IF LEFT$(J$,1)="N" THEN GOTO 1960
1940 C=C+50:O=O/3
1950 RETURN
1960 PRINT "I'M BUSTED. CONGRATULATIONS!":STOP
1970 PRINT H$;I$;
1980 IF H$<>"A FLUS" THEN GOTO 2000
1990 K=INT(K/100):GOSUB 1150:PRINT :RETURN
2000 K=FNB(K):GOSUB 1080:IF H$="SCHMAL" THEN GOTO 2020
2010 IF H$<>"STRAIG" THEN GOTO 2030
2020 PRINT " HIGH":RETURN
2030 PRINT "S":RETURN
2040 PRINT :PRINT "YOU CAN'T BET WHAT YOU HAVEN'T GOT"
2050 IF O/2=INT(O/2) THEN GOTO 2110
2060 INPUT "WOULD YOU LIKE TO SELL YOUR WATCH";J$:IF LEFT$(J$,1)="N" THEN GOTO 2110
2070 IF FNA(0)>=7 THEN GOTO 2090
2080 PRINT "I'LL GIVE YOU $75 FOR IT":S=S+75:GOTO 2100
2090 PRINT "THAT'S A PRETTY CRUMMY WATCH - I'LL GIVE YOU $25":S=S+25
2100 O=O*2:RETURN
2110 IF O/3<>INT(O/3) THEN GOTO 2180
2120 INPUT "WILL YOU PART WITH THAT DIAMOND TIE PIN";J$:IF LEFT$(J$,1)="N" THEN GOTO 2170
2130 IF FNA(0)>=6 THEN GOTO 2150
2140 PRINT "YOU ARE NOW $100 RICHER":S=S+100:GOTO 2160
2150 PRINT "IT'S PASTE. $25":S=S+25
2160 O=O*3
2170 RETURN
2180 PRINT "YOUR WAD IS SHOT. SO LONG, SUCKER!"
2190 END

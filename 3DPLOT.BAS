0 DEF FNZ(X,Y)=Y*Y-X*X
10 KEY OFF
20 CLS:PRINT"        3-D PLOT"
30  PRINT "        === ====":PRINT :PRINT
40 INPUT "HAVE YOU DEFINED THE FUNCTION TO BE PLOTTED Y/N";A$
50 IF A$="Y" THEN GOTO 110
60 CLS:PRINT "You must define the function to be plotted in line 0 of the program. It"
70 PRINT "must be called FNZ(X,Y). For example, a function to plot a saddle curve is:"
80 PRINT :PRINT "       FNZ(X,Y)=Y*Y-X*X":PRINT
90 PRINT "To define the function, key in 0 (the line number), followed by DEF and the"
100 PRINT "function (e.g. 0 DEF FNZ(X,Y)=Y*Y-X*X  ) and then rerun the program.":PRINT :END
110 CLS:PRINT "X VALUES":INPUT "FROM";BX:INPUT "TO";TX
120 PRINT "Y VALUES":INPUT "FROM";BY:INPUT "TO";TY
130 PRINT "VIEW FROM":INPUT "X";X:INPUT "Y";Y:INPUT "Z";Z
140 PRINT "LOOKING TOWARDS":INPUT "X";PX:INPUT "Y";PY:INPUT "Z";PZ
150 VX=X-PX+.5:VY=Y-PY+.5:VZ=Z-PZ+.5
160 S=VX*VX+VY*VY:T=S+VZ*VZ
170 R=SQR(S):Q=SQR(T)
180 X=BX:Y=BY:GOSUB 360:MNX=G:MXX=G:MNY=H:MXY=H
190 FOR X=BX TO TX:FOR Y=BY TO TY:GOSUB 360
200 IF G>MXX THEN MXX=G
210 IF G<MNX THEN MNX=G
220 IF H>MXY THEN MXY=H
230 IF H<MNY THEN MNY=H
240 NEXT :NEXT
241 CLS:PRINT "SELECT COLOUR":PRINT :PRINT "1 - GREEN":PRINT "2 - RED":PRINT"3 - BROWN":PRINT "4 - CYAN":PRINT "5 - MAGENTA":PRINT "6 - WHITE":PRINT :INPUT "NUMBER";CL
242 PL=0:IF CL>3 THEN PL=1:CL=CL-3
250 RG=MXX-MNX+.1:SCX=300/RG:OX=6-MNX*SCX
260 RG=MXY-MNY+.1:SCY=180/RG:OY=MNY*SCY+194
270 CLS:SCREEN 1:COLOR 0,PL
280 FOR X=BX TO TX:Y=BY:GOSUB 420:PRESET (G,H)
290 FOR Y=BY TO TY:GOSUB 420
300 IF O>.1 THEN LINE -(G,H),CL
310 NEXT:NEXT
320 FOR Y=BY TO TY:X=BX:GOSUB 420:PRESET (G,H)
330 FOR X=BX TO TX:GOSUB 420
340 IF O>.1 THEN LINE -(G,H),CL
350 NEXT:NEXT:INPUT A$:GOTO 20
360 Z=FNZ(X,Y)
380 O=(T-X*VX-Y*VY-Z*VZ)*R 'Distance from eye
385 IF O=0 THEN O=9.000001E-02 'Prevent div by zero
390 G=(Y*VX-X*VY)*Q/O
400 H=(Z*S-VZ*(X*VX+Y*VY))/O
410 RETURN
420 GOSUB 360
430 G=INT(SCX*G+OX):H=INT(OY-SCY*H)
440 RETURN
